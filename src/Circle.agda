------------------------------------------------------------------------
-- The "circle"
------------------------------------------------------------------------

{-# OPTIONS --cubical --safe #-}

-- Partly following the HoTT book.

-- The module is parametrised by a notion of equality. The higher
-- constructor of the HIT defining the circle uses path equality, but
-- the supplied notion of equality is used for many other things.

import Equality.Path as P

module Circle {eâº} (eq : âˆ€ {a p} â†’ P.Equality-with-paths a p eâº) where

open P.Derived-definitions-and-properties eq hiding (elim)

open import Prelude

open import Bijection equality-with-J as Bijection using (_â†”_)
import Bijection P.equality-with-J as PB
open import Equality.Path.Isomorphisms eq
import Equality.Path.Isomorphisms P.equality-with-paths as PI
open import Equivalence equality-with-J as Eq using (_â‰ƒ_)
open import Function-universe equality-with-J as F hiding (id; _âˆ˜_)
open import H-level equality-with-J
open import H-level.Closure equality-with-J
open import H-level.Truncation.Propositional eq as Trunc
  using (âˆ¥_âˆ¥; âˆ£_âˆ£)
open import Nat equality-with-J
open import Univalence-axiom equality-with-J using (Â¬-Type-set)

private
  variable
    a p : Level
    A   : Type p
    P   : A â†’ Type p
    b â„“ : A

------------------------------------------------------------------------
-- The type and some eliminators

-- The circle.

data ğ•ŠÂ¹ : Type where
  base  : ğ•ŠÂ¹
  loopá´¾ : base P.â‰¡ base

loop : base â‰¡ base
loop = _â†”_.from â‰¡â†”â‰¡ loopá´¾

-- A dependent eliminator, expressed using paths.

elimá´¾ :
  (P : ğ•ŠÂ¹ â†’ Type p)
  (b : P base) â†’
  P.[ (Î» i â†’ P (loopá´¾ i)) ] b â‰¡ b â†’
  (x : ğ•ŠÂ¹) â†’ P x
elimá´¾ P b â„“ base      = b
elimá´¾ P b â„“ (loopá´¾ i) = â„“ i

-- A non-dependent eliminator, expressed using paths.

recá´¾ : (b : A) â†’ b P.â‰¡ b â†’ ğ•ŠÂ¹ â†’ A
recá´¾ = elimá´¾ _

-- A dependent eliminator.

elim :
  (P : ğ•ŠÂ¹ â†’ Type p)
  (b : P base) â†’
  subst P loop b â‰¡ b â†’
  (x : ğ•ŠÂ¹) â†’ P x
elim P b â„“ = elimá´¾ P b (substâ‰¡â†’[]â‰¡ â„“)

-- A "computation" rule.

elim-loop : dcong (elim P b â„“) loop â‰¡ â„“
elim-loop = dcong-substâ‰¡â†’[]â‰¡ (refl _)

-- A non-dependent eliminator.

rec : (b : A) â†’ b â‰¡ b â†’ ğ•ŠÂ¹ â†’ A
rec b â„“ = recá´¾ b (_â†”_.to â‰¡â†”â‰¡ â„“)

-- A "computation" rule.

rec-loop : cong (rec b â„“) loop â‰¡ â„“
rec-loop = cong-â‰¡â†”â‰¡ (refl _)

-- An alternative non-dependent eliminator.

recâ€² : (b : A) â†’ b â‰¡ b â†’ ğ•ŠÂ¹ â†’ A
recâ€² {A = A} b â„“ = elim
  (const A)
  b
  (subst (const A) loop b  â‰¡âŸ¨ subst-const _ âŸ©
   b                       â‰¡âŸ¨ â„“ âŸ©âˆ
   b                       âˆ)

-- A "computation" rule.

recâ€²-loop : cong (recâ€² b â„“) loop â‰¡ â„“
recâ€²-loop = dcongâ‰¡â†’congâ‰¡ elim-loop

------------------------------------------------------------------------
-- Some negative results

-- The equality loop is not equal to reflÂ base.

loopâ‰¢refl : loop â‰¢ refl base
loopâ‰¢refl loopâ‰¡refl = Â¬-Type-set univ Type-set
  where
  reflâ‰¡ : (A : Type) (Aâ‰¡A : A â‰¡ A) â†’ refl A â‰¡ Aâ‰¡A
  reflâ‰¡ A Aâ‰¡A =
    refl A                        â‰¡âŸ¨âŸ©
    refl (rec A Aâ‰¡A base)         â‰¡âŸ¨ sym $ cong-refl _ âŸ©
    cong (rec A Aâ‰¡A) (refl base)  â‰¡âŸ¨ cong (cong (rec A Aâ‰¡A)) $ sym loopâ‰¡refl âŸ©
    cong (rec A Aâ‰¡A) loop         â‰¡âŸ¨ rec-loop âŸ©âˆ
    Aâ‰¡A                           âˆ

  Type-set : Is-set Type
  Type-set {x = A} {y = B} =
    elimÂ¹ (Î» p â†’ âˆ€ q â†’ p â‰¡ q)
          (reflâ‰¡ A)

-- Thus the circle is not a set.

Â¬-ğ•ŠÂ¹-set : Â¬ Is-set ğ•ŠÂ¹
Â¬-ğ•ŠÂ¹-set =
  Is-set ğ•ŠÂ¹                     â†âŸ¨ (Î» h â†’ h) âŸ©
  Is-proposition (base â‰¡ base)  â†âŸ¨ (Î» h â†’ h _ _) âŸ©
  loop â‰¡ refl base              â†âŸ¨ loopâ‰¢refl âŸ©â–¡
  âŠ¥                             â–¡

-- A function with the type of refl (for ğ•ŠÂ¹) that is not equal to
-- refl.

not-refl : (x : ğ•ŠÂ¹) â†’ x â‰¡ x
not-refl = elim _
  loop
  (subst (Î» z â†’ z â‰¡ z) loop loop  â‰¡âŸ¨ â‰¡â‡’â† _ (sym [substâ‰¡]â‰¡[transâ‰¡trans]) (refl _) âŸ©âˆ
   loop                           âˆ)

-- The function not-refl is not equal to refl.

not-reflâ‰¢refl : not-refl â‰¢ refl
not-reflâ‰¢refl =
  not-refl â‰¡ refl   â†âŸ¨ cong (_$ _) âŸ©
  loop â‰¡ refl base  â†âŸ¨ loopâ‰¢refl âŸ©â–¡
  âŠ¥                 â–¡

-- There is a value with the type of refl that is not equal to refl.

âˆƒâ‰¢refl : âˆƒ Î» (f : (x : ğ•ŠÂ¹) â†’ x â‰¡ x) â†’ f â‰¢ refl
âˆƒâ‰¢refl = not-refl , not-reflâ‰¢refl

-- For every universe level there is a type A such that
-- (xÂ :Â A)Â â†’Â xÂ â‰¡Â x is not a proposition.

Â¬-type-of-refl-propositional :
  âˆƒ Î» (A : Type a) â†’ Â¬ Is-proposition ((x : A) â†’ x â‰¡ x)
Â¬-type-of-refl-propositional {a = a} =
    â†‘ _ ğ•ŠÂ¹
  , (Is-proposition (âˆ€ x â†’ x â‰¡ x)                                 â†âŸ¨ (Î» prop â†’ prop _ _) âŸ©

     cong lift âˆ˜ projâ‚ âˆƒâ‰¢refl âˆ˜ lower â‰¡ cong lift âˆ˜ refl âˆ˜ lower  â†âŸ¨ cong (_âˆ˜ lift) âŸ©

     cong lift âˆ˜ projâ‚ âˆƒâ‰¢refl â‰¡ cong lift âˆ˜ refl                  â†âŸ¨ cong (cong lower âˆ˜_) âŸ©

     cong lower âˆ˜ cong lift âˆ˜ projâ‚ âˆƒâ‰¢refl â‰¡
     cong lower âˆ˜ cong lift âˆ˜ refl                                â†âŸ¨ â‰¡â‡’â† _ (congâ‚‚ _â‰¡_ (âŸ¨extâŸ© Î» _ â†’ cong-âˆ˜ _ _ _) (âŸ¨extâŸ© Î» _ â†’ cong-âˆ˜ _ _ _)) âŸ©

     cong id âˆ˜ projâ‚ âˆƒâ‰¢refl â‰¡ cong id âˆ˜ refl                      â†âŸ¨ â‰¡â‡’â† _ (sym $ congâ‚‚ _â‰¡_ (âŸ¨extâŸ© Î» _ â†’ cong-id _) (âŸ¨extâŸ© Î» _ â†’ cong-id _)) âŸ©

     projâ‚ âˆƒâ‰¢refl â‰¡ refl                                          â†âŸ¨ projâ‚‚ âˆƒâ‰¢refl âŸ©â–¡

     âŠ¥                                                            â–¡)

-- Every element of the circle is /merely/ equal to the base point.
--
-- This lemma was mentioned by Mike Shulman in a blog post
-- (http://homotopytypetheory.org/2013/07/24/cohomology/).

all-points-on-the-circle-are-merely-equal :
  (x : ğ•ŠÂ¹) â†’ âˆ¥ x â‰¡ base âˆ¥
all-points-on-the-circle-are-merely-equal =
  elim _
       âˆ£ refl base âˆ£
       (Trunc.truncation-is-proposition _ _)

-- Thus every element of the circle is not not equal to the base
-- point.

all-points-on-the-circle-are-Â¬Â¬-equal :
  (x : ğ•ŠÂ¹) â†’ Â¬ Â¬ x â‰¡ base
all-points-on-the-circle-are-Â¬Â¬-equal x =
  x â‰¢ base        â†âŸ¨ Trunc.rec âŠ¥-propositional âŸ©
  Â¬ âˆ¥ x â‰¡ base âˆ¥  â†âŸ¨ _$ all-points-on-the-circle-are-merely-equal x âŸ©â–¡
  âŠ¥               â–¡

-- It is not the case that every point on the circle is equal to the
-- base point.

Â¬-all-points-on-the-circle-are-equal :
  Â¬ ((x : ğ•ŠÂ¹) â†’ x â‰¡ base)
Â¬-all-points-on-the-circle-are-equal =
  ((x : ğ•ŠÂ¹) â†’ x â‰¡ base)  â†âŸ¨ (Î» hyp x y â†’ x     â‰¡âŸ¨ hyp x âŸ©
                                         base  â‰¡âŸ¨ sym (hyp y) âŸ©âˆ
                                         y     âˆ) âŸ©
  Is-proposition ğ•ŠÂ¹      â†âŸ¨ monoâ‚ 1 âŸ©
  Is-set ğ•ŠÂ¹              â†âŸ¨ Â¬-ğ•ŠÂ¹-set âŸ©â–¡
  âŠ¥                      â–¡

-- Thus double-negation shift for Type-valued predicates over ğ•ŠÂ¹ does
-- not hold in general.

Â¬-double-negation-shift :
  Â¬ ({P : ğ•ŠÂ¹ â†’ Type} â†’ ((x : ğ•ŠÂ¹) â†’ Â¬ Â¬ P x) â†’ Â¬ Â¬ ((x : ğ•ŠÂ¹) â†’ P x))
Â¬-double-negation-shift =
  ({P : ğ•ŠÂ¹ â†’ Type} â†’ ((x : ğ•ŠÂ¹) â†’ Â¬ Â¬ P x) â†’ Â¬ Â¬ ((x : ğ•ŠÂ¹) â†’ P x))  â†âŸ¨ _$ all-points-on-the-circle-are-Â¬Â¬-equal âŸ©
  Â¬ Â¬ ((x : ğ•ŠÂ¹) â†’ x â‰¡ base)                                        â†âŸ¨ _$ Â¬-all-points-on-the-circle-are-equal âŸ©â–¡
  âŠ¥                                                                â–¡

-- Furthermore excluded middle for arbitrary types (in Type) does not
-- hold.

Â¬-excluded-middle : Â¬ ({A : Type} â†’ Dec A)
Â¬-excluded-middle =
  ({A : Type} â†’ Dec A)                                             â†âŸ¨ (Î» em Â¬Â¬a â†’ [ id , âŠ¥-elim âˆ˜ Â¬Â¬a ] em) âŸ©
  ({A : Type} â†’ Â¬ Â¬ A â†’ A)                                         â†âŸ¨ (Î» dne â†’ flip _$_ âˆ˜ (dne âˆ˜_)) âŸ©
  ({P : ğ•ŠÂ¹ â†’ Type} â†’ ((x : ğ•ŠÂ¹) â†’ Â¬ Â¬ P x) â†’ Â¬ Â¬ ((x : ğ•ŠÂ¹) â†’ P x))  â†âŸ¨ Â¬-double-negation-shift âŸ©â–¡
  âŠ¥                                                                â–¡

-- H-level.Closure.projâ‚-closure cannot be generalised by replacing
-- the assumption âˆ€Â aÂ â†’Â BÂ a with âˆ€Â aÂ â†’Â âˆ¥Â BÂ aÂ âˆ¥.
--
-- This observation is due to Andrea Vezzosi.

Â¬-generalised-projâ‚-closure :
  Â¬ ({A : Type} {B : A â†’ Type} â†’
     (âˆ€ a â†’ âˆ¥ B a âˆ¥) â†’
     âˆ€ n â†’ H-level n (Î£ A B) â†’ H-level n A)
Â¬-generalised-projâ‚-closure generalised-projâ‚-closure =
                                 $âŸ¨ singleton-contractible _ âŸ©
  Contractible (Î£ ğ•ŠÂ¹ (_â‰¡ base))  â†âŸ¨ generalised-projâ‚-closure
                                      all-points-on-the-circle-are-merely-equal
                                      0 âŸ©
  Contractible ğ•ŠÂ¹                â†âŸ¨ mono (zeroâ‰¤ 2) âŸ©
  Is-set ğ•ŠÂ¹                      â†âŸ¨ Â¬-ğ•ŠÂ¹-set âŸ©â–¡
  âŠ¥                              â–¡

------------------------------------------------------------------------
-- An alternative approach to defining eliminators and proving
-- computation rules for arbitrary notions of equality, based on an
-- anonymous reviewer's suggestion

-- CircleÂ eqÂ p is an axiomatisation of the circle, for the given
-- notion of equality eq, eliminating into TypeÂ p.
--
-- Note that the statement of the computation rule for "loop" is more
-- complicated than above (elim-loop). The reason is that the
-- computation rule for "base" does not hold definitionally.

Circle :
  âˆ€ {eâº} â†’
  (âˆ€ {a p} â†’ P.Equality-with-paths a p eâº) â†’
  (p : Level) â†’ Type (lsuc p)
Circle eq p =
  âˆƒ Î» (ğ•ŠÂ¹ : Type) â†’
  âˆƒ Î» (base : ğ•ŠÂ¹) â†’
  âˆƒ Î» (loop : base E.â‰¡ base) â†’
    (P : ğ•ŠÂ¹ â†’ Type p)
    (b : P base)
    (â„“ : E.subst P loop b E.â‰¡ b) â†’
    âˆƒ Î» (elim : (x : ğ•ŠÂ¹) â†’ P x) â†’
    âˆƒ Î» (elim-base : elim base E.â‰¡ b) â†’
      E.subst (Î» b â†’ E.subst P loop b E.â‰¡ b)
              elim-base
              (E.dcong elim loop)
        E.â‰¡
      â„“
  where
  module E = P.Derived-definitions-and-properties eq

-- A circle defined for paths (P.equality-with-J) is equivalent to one
-- defined for eq.

Circleâ‰ƒCircle : Circle P.equality-with-paths p â‰ƒ Circle eq p
Circleâ‰ƒCircle =
  âˆƒ-cong Î» _ â†’
  âˆƒ-cong Î» _ â†’
  Î£-cong (inverse â‰¡â†”â‰¡) Î» loop â†’
  âˆ€-cong ext Î» P â†’
  âˆ€-cong ext Î» b â†’
  Î -cong-contra ext substâ‰¡â†”substâ‰¡ Î» â„“ â†’
  âˆƒ-cong Î» f â†’
  Î£-cong (inverse â‰¡â†”â‰¡) Î» f-base â†’
  let lemma = P.elimÂ¹
        (Î» eq â†’ _â†”_.from substâ‰¡â†”substâ‰¡
                  (P.subst
                     (Î» b â†’ P.subst P loop b P.â‰¡ b)
                     eq
                     (P.dcong f loop)) â‰¡
                P.subst
                  (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
                  eq
                  (_â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop)))
        (_â†”_.from substâ‰¡â†”substâ‰¡
           (P.subst
              (Î» b â†’ P.subst P loop b P.â‰¡ b)
              P.refl
              (P.dcong f loop))                       â‰¡âŸ¨ cong (_â†”_.from substâ‰¡â†”substâ‰¡) $ _â†”_.from â‰¡â†”â‰¡ $
                                                         P.subst-refl (Î» b â†’ P.subst P loop b P.â‰¡ b) _ âŸ©

         _â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop)      â‰¡âŸ¨ sym $ _â†”_.from â‰¡â†”â‰¡ $
                                                         P.subst-refl (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b) _ âŸ©âˆ
         P.subst
           (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
           P.refl
           (_â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop))  âˆ)
        _
  in
  P.subst
    (Î» b â†’ P.subst P loop b P.â‰¡ b)
    f-base
    (P.dcong f loop) P.â‰¡
  _â†”_.to substâ‰¡â†”substâ‰¡ â„“                           â†”âŸ¨ â‰¡â†”â‰¡ F.âˆ˜ inverse (fromâ‰¡â†”â‰¡to (Eq.â†”â‡’â‰ƒ substâ‰¡â†”substâ‰¡)) F.âˆ˜ inverse â‰¡â†”â‰¡ âŸ©

  _â†”_.from substâ‰¡â†”substâ‰¡
    (P.subst
       (Î» b â†’ P.subst P loop b P.â‰¡ b)
       f-base
       (P.dcong f loop)) P.â‰¡
  â„“                                                â†âŸ¨ â‰¡â‡’â† _ (cong (P._â‰¡ _) lemma) âŸ©

  P.subst
    (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
    f-base
    (_â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop)) P.â‰¡
  â„“                                                â†âŸ¨ â‰¡â‡’â† _ $ cong (Î» eq â†’ P.subst (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b) f-base eq P.â‰¡ â„“) $
                                                      _â†”_.from-to (inverse substâ‰¡â†”substâ‰¡) dcongâ‰¡dcong âŸ©
  P.subst
    (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
    f-base
    (dcong f (_â†”_.from â‰¡â†”â‰¡ loop)) P.â‰¡
  â„“                                                â†”âŸ¨ inverse substâ‰¡â†”substâ‰¡ âŸ©â–¡

  subst
    (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
    (_â†”_.from â‰¡â†”â‰¡ f-base)
    (dcong f (_â†”_.from â‰¡â†”â‰¡ loop)) â‰¡
  â„“                                                â–¡

-- An implemention of the circle for paths (P.equality-with-paths).

circleá´¾ : Circle P.equality-with-paths p
circleá´¾ =
    ğ•ŠÂ¹
  , base
  , loopá´¾
  , Î» P b â„“ â†’
      let elim = elimá´¾ P b (PI.substâ‰¡â†’[]â‰¡ {B = P} â„“)
      in
        elim
      , P.refl
      , (P.subst (Î» b â†’ P.subst P loopá´¾ b P.â‰¡ b) P.refl
           (P.dcong elim loopá´¾)                          P.â‰¡âŸ¨ P.subst-refl (Î» b â†’ P.subst P loopá´¾ b P.â‰¡ b) _ âŸ©

         P.dcong elim loopá´¾                              P.â‰¡âŸ¨ PI.dcong-substâ‰¡â†’[]â‰¡ {f = elim} P.refl âŸ©âˆ

         â„“                                               âˆ)

-- An implementation of the circle for eq.

circle : Circle eq p
circle = _â‰ƒ_.to Circleâ‰ƒCircle circleá´¾

-- The latter implementation computes in the right way for "base".

_ :
  let _ , baseâ€² , _ , elimâ€² = circle {p = p} in
  âˆ€ {P b â„“} â†’
  projâ‚ (elimâ€² P b â„“) baseâ€² â‰¡ b
_ = refl _

-- The usual computation rule for "loop" can be derived.

elim-loop-circle :
  let _ , _ , loopâ€² , elimâ€² = circle {p = p} in
  âˆ€ {P b â„“} â†’
  dcong (projâ‚ (elimâ€² P b â„“)) loopâ€² â‰¡ â„“
elim-loop-circle {P = P} {b = b} {â„“ = â„“} =
  let _ , _ , loopâ€² , elimâ€²           = circle
      elimâ€³ , elimâ€³-base , elimâ€³-loop = elimâ€² P b â„“

      lemma =
        refl _               â‰¡âŸ¨ sym from-â‰¡â†”â‰¡-refl âŸ©
        _â†”_.from â‰¡â†”â‰¡ P.refl  â‰¡âŸ¨âŸ©
        elimâ€³-base           âˆ
  in
  dcong elimâ€³ loopâ€²                                                 â‰¡âŸ¨ sym $ subst-refl _ _ âŸ©
  subst (Î» b â†’ subst P loopâ€² b â‰¡ b) (refl _) (dcong elimâ€³ loopâ€²)    â‰¡âŸ¨ cong (Î» eq â†’ subst (Î» b â†’ subst P loopâ€² b â‰¡ b) eq (dcong elimâ€³ loopâ€²)) lemma âŸ©
  subst (Î» b â†’ subst P loopâ€² b â‰¡ b) elimâ€³-base (dcong elimâ€³ loopâ€²)  â‰¡âŸ¨ elimâ€³-loop âŸ©âˆ
  â„“                                                                 âˆ

-- An alternative to Circleâ‰ƒCircle that does not give the "right"
-- computational behaviour for circleâ€² below.

Circleâ‰ƒCircleâ€² : Circle P.equality-with-paths p â‰ƒ Circle eq p
Circleâ‰ƒCircleâ€² =
  âˆƒ-cong Î» _ â†’
  âˆƒ-cong Î» _ â†’
  Î£-cong (inverse â‰¡â†”â‰¡) Î» loop â†’
  âˆ€-cong ext Î» P â†’
  âˆ€-cong ext Î» b â†’
  Î -cong ext (inverse substâ‰¡â†”substâ‰¡) Î» â„“ â†’
  âˆƒ-cong Î» f â†’
  Î£-cong (inverse â‰¡â†”â‰¡) Î» f-base â†’
  let lemma = P.elimÂ¹
        (Î» eq â†’ _â†”_.from substâ‰¡â†”substâ‰¡
                  (P.subst
                     (Î» b â†’ P.subst P loop b P.â‰¡ b)
                     eq
                     (P.dcong f loop)) â‰¡
                P.subst
                  (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
                  eq
                  (_â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop)))
        (_â†”_.from substâ‰¡â†”substâ‰¡
           (P.subst
              (Î» b â†’ P.subst P loop b P.â‰¡ b)
              P.refl
              (P.dcong f loop))                       â‰¡âŸ¨ cong (_â†”_.from substâ‰¡â†”substâ‰¡) $ _â†”_.from â‰¡â†”â‰¡ $
                                                         P.subst-refl (Î» b â†’ P.subst P loop b P.â‰¡ b) _ âŸ©

         _â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop)      â‰¡âŸ¨ sym $ _â†”_.from â‰¡â†”â‰¡ $
                                                         P.subst-refl (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b) _ âŸ©âˆ
         P.subst
           (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
           P.refl
           (_â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop))  âˆ)
        _
  in
  P.subst
    (Î» b â†’ P.subst P loop b P.â‰¡ b)
    f-base
    (P.dcong f loop) P.â‰¡
  â„“                                                â†”âŸ¨ â‰¡â†”â‰¡ F.âˆ˜ from-isomorphism (inverse $ Eq.â‰ƒ-â‰¡ $ Eq.â†”â‡’â‰ƒ $ inverse substâ‰¡â†”substâ‰¡) F.âˆ˜ inverse â‰¡â†”â‰¡ âŸ©

  _â†”_.from substâ‰¡â†”substâ‰¡
    (P.subst
       (Î» b â†’ P.subst P loop b P.â‰¡ b)
       f-base
       (P.dcong f loop)) P.â‰¡
  _â†”_.from substâ‰¡â†”substâ‰¡ â„“                         â†âŸ¨ â‰¡â‡’â† _ (cong (P._â‰¡ _â†”_.from substâ‰¡â†”substâ‰¡ â„“) lemma) âŸ©

  P.subst
    (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
    f-base
    (_â†”_.from substâ‰¡â†”substâ‰¡ (P.dcong f loop)) P.â‰¡
  _â†”_.from substâ‰¡â†”substâ‰¡ â„“                         â†âŸ¨ â‰¡â‡’â† _ $ cong (Î» eq â†’ P.subst (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b) f-base eq P.â‰¡ _â†”_.from substâ‰¡â†”substâ‰¡ â„“) $
                                                      _â†”_.from-to (inverse substâ‰¡â†”substâ‰¡) dcongâ‰¡dcong âŸ©
  P.subst
    (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
    f-base
    (dcong f (_â†”_.from â‰¡â†”â‰¡ loop)) P.â‰¡
  _â†”_.from substâ‰¡â†”substâ‰¡ â„“                         â†”âŸ¨ inverse substâ‰¡â†”substâ‰¡ âŸ©â–¡

  subst
    (Î» b â†’ subst P (_â†”_.from â‰¡â†”â‰¡ loop) b â‰¡ b)
    (_â†”_.from â‰¡â†”â‰¡ f-base)
    (dcong f (_â†”_.from â‰¡â†”â‰¡ loop)) â‰¡
  _â†”_.from substâ‰¡â†”substâ‰¡ â„“                         â–¡

-- An alternative implementation of the circle for eq.

circleâ€² : Circle eq p
circleâ€² = _â‰ƒ_.to Circleâ‰ƒCircleâ€² circleá´¾

-- This implementation does not compute in the right way for "base".
-- The following code is (at the time of writing) rejected by Agda.

-- _ :
--   let _ , baseâ€² , _ , elimâ€² = circleâ€² {p = p} in
--   âˆ€ {P b â„“} â†’
--   projâ‚ (elimâ€² P b â„“) baseâ€² â‰¡ b
-- _ = refl _
