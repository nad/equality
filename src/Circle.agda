------------------------------------------------------------------------
-- The "circle"
------------------------------------------------------------------------

{-# OPTIONS --cubical --safe #-}

-- Partly following the HoTT book.

-- The module is parametrised by a notion of equality. The higher
-- constructor of the HIT defining the circle uses path equality, but
-- the supplied notion of equality is used for many other things.

open import Equality

module Circle
  {reflexive} (eq : âˆ€ {a p} â†’ Equality-with-J a p reflexive) where

import Equality.Path as P
open import Prelude

open import Bijection eq using (_â†”_)
open Derived-definitions-and-properties eq hiding (elim)
open import Equality.Path.Isomorphisms eq
open import Function-universe eq
open import H-level eq
open import H-level.Truncation.Propositional eq hiding (elim; rec)
open import Nat eq
open import Univalence-axiom eq

private
  variable
    p : Level
    A : Set p

-- The circle.

data ğ•ŠÂ¹ : Set where
  base  : ğ•ŠÂ¹
  loopâ€² : base P.â‰¡ base

loop : base â‰¡ base
loop = _â†”_.from â‰¡â†”â‰¡ loopâ€²

-- A dependent eliminator.

module Elim
  (P : ğ•ŠÂ¹ â†’ Set p)
  (b : P base)
  (â„“ : subst P loop b â‰¡ b)
  where

  elim : (x : ğ•ŠÂ¹) â†’ P x
  elim base      = b
  elim (loopâ€² i) = substâ‰¡â†’[]â‰¡ â„“ i

  -- "Computation" rule for loop.

  elim-loop : dcong elim loop â‰¡ â„“
  elim-loop = dcong-substâ‰¡â†’[]â‰¡ (refl _)

open Elim public

-- A non-dependent eliminator.

module Rec (b : A) (â„“ : b â‰¡ b) where

  rec : ğ•ŠÂ¹ â†’ A
  rec base      = b
  rec (loopâ€² i) = _â†”_.to â‰¡â†”â‰¡ â„“ i

  rec-loop : cong rec loop â‰¡ â„“
  rec-loop = cong-â‰¡â†”â‰¡ (refl _)

private

  -- An alternative non-dependent eliminator.

  module Recâ€² (b : A) (â„“ : b â‰¡ b) where

    private
      module E = Elim
        (const A)
        b
        (subst (const A) loop b  â‰¡âŸ¨ subst-const _ âŸ©
         b                       â‰¡âŸ¨ â„“ âŸ©âˆ
         b                       âˆ)

    rec : ğ•ŠÂ¹ â†’ A
    rec = E.elim

    rec-loop : cong rec loop â‰¡ â„“
    rec-loop = dcongâ‰¡â†’congâ‰¡ E.elim-loop

open Rec public

-- The equality loop is not equal to reflÂ base.

loopâ‰¢refl : loop â‰¢ refl base
loopâ‰¢refl loopâ‰¡refl = Â¬-Set-set univ Set-set
  where
  reflâ‰¡ : (A : Set) (Aâ‰¡A : A â‰¡ A) â†’ refl A â‰¡ Aâ‰¡A
  reflâ‰¡ A Aâ‰¡A =
    refl A                  â‰¡âŸ¨âŸ©
    refl (R.rec base)       â‰¡âŸ¨ sym $ cong-refl _ âŸ©
    cong R.rec (refl base)  â‰¡âŸ¨ cong (cong R.rec) $ sym loopâ‰¡refl âŸ©
    cong R.rec loop         â‰¡âŸ¨ R.rec-loop âŸ©âˆ
    Aâ‰¡A                     âˆ
    where
    module R = Rec A Aâ‰¡A

  Set-set : Is-set Set
  Set-set {x = A} {y = B} =
    elimÂ¹ (Î» p â†’ âˆ€ q â†’ p â‰¡ q)
          (reflâ‰¡ A)

-- Thus the circle is not a set.

Â¬-ğ•ŠÂ¹-set : Â¬ Is-set ğ•ŠÂ¹
Â¬-ğ•ŠÂ¹-set =
  Is-set ğ•ŠÂ¹                     â†âŸ¨ (Î» h â†’ h) âŸ©
  Is-proposition (base â‰¡ base)  â†âŸ¨ (Î» h â†’ h _ _) âŸ©
  loop â‰¡ refl base              â†âŸ¨ loopâ‰¢refl âŸ©â–¡
  âŠ¥                             â–¡

-- Every element of the circle is /merely/ equal to the base point.
--
-- This lemma was mentioned by Mike Shulman in a blog post
-- (http://homotopytypetheory.org/2013/07/24/cohomology/).

all-points-on-the-circle-are-merely-equal :
  (x : ğ•ŠÂ¹) â†’ âˆ¥ x â‰¡ base âˆ¥
all-points-on-the-circle-are-merely-equal =
  elim _
       âˆ£ refl base âˆ£
       (truncation-is-proposition _ _)

-- However, it is not the case that every point on the circle is
-- /equal/ to the base point.

Â¬-all-points-on-the-circle-are-equal :
  Â¬ ((x : ğ•ŠÂ¹) â†’ x â‰¡ base)
Â¬-all-points-on-the-circle-are-equal =
  ((x : ğ•ŠÂ¹) â†’ x â‰¡ base)  â†âŸ¨ (Î» hyp x y â†’ x     â‰¡âŸ¨ hyp x âŸ©
                                         base  â‰¡âŸ¨ sym (hyp y) âŸ©âˆ
                                         y     âˆ) âŸ©
  Is-proposition ğ•ŠÂ¹      â†âŸ¨ monoâ‚ 1 âŸ©
  Is-set ğ•ŠÂ¹              â†âŸ¨ Â¬-ğ•ŠÂ¹-set âŸ©â–¡
  âŠ¥                      â–¡

-- H-level.Closure.projâ‚-closure cannot be generalised by replacing
-- the assumption âˆ€Â aÂ â†’Â BÂ a with âˆ€Â aÂ â†’Â âˆ¥Â BÂ aÂ âˆ¥.
--
-- This observation is due to Andrea Vezzosi.

Â¬-generalised-projâ‚-closure :
  Â¬ ({A : Set} {B : A â†’ Set} â†’
     (âˆ€ a â†’ âˆ¥ B a âˆ¥) â†’
     âˆ€ n â†’ H-level n (Î£ A B) â†’ H-level n A)
Â¬-generalised-projâ‚-closure generalised-projâ‚-closure =
                                 $âŸ¨ singleton-contractible _ âŸ©
  Contractible (Î£ ğ•ŠÂ¹ (_â‰¡ base))  â†âŸ¨ generalised-projâ‚-closure
                                      all-points-on-the-circle-are-merely-equal
                                      0 âŸ©
  Contractible ğ•ŠÂ¹                â†âŸ¨ mono (zeroâ‰¤ 2) âŸ©
  Is-set ğ•ŠÂ¹                      â†âŸ¨ Â¬-ğ•ŠÂ¹-set âŸ©â–¡
  âŠ¥                              â–¡
