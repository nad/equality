------------------------------------------------------------------------
-- Truncation, defined as a HIT
------------------------------------------------------------------------

{-# OPTIONS --cubical --safe #-}

-- The beginning of this module follows the HoTT book rather closely.

-- The module is parametrised by a notion of equality. The higher
-- constructor of the HIT defining the truncation uses path equality,
-- but the supplied notion of equality is used for many other things.

open import Equality

module H-level.Truncation
  {reflexive} (eq : âˆ€ {a p} â†’ Equality-with-J a p reflexive) where

open Derived-definitions-and-properties eq hiding (elim)

import Equality.Path as P
open import Logical-equivalence using (_â‡”_)
open import Prelude

open import Bijection eq using (_â†”_)
open import Equality.Path.Isomorphisms eq
import Equivalence eq as Eq
open import Function-universe eq hiding (id; _âˆ˜_)
open import H-level eq
open import H-level.Truncation.Propositional eq as TP using (âˆ¥_âˆ¥)
open import Monad eq
open import Nat eq as Nat using (_â‰¤_; min)
open import Pointed-type eq
open import Sphere eq
open import Suspension eq as Susp using (north)

private
  variable
    a â„“ p : Level
    A B   : Set a
    P     : A â†’ Set p
    x     : A
    f g r : A â†’ B
    m n   : â„•
    k     : Isomorphism-kind

-- A truncation operator for positive h-levels.

data âˆ¥_âˆ¥[1+_] (A : Set a) (n : â„•) : Set a where
  âˆ£_âˆ£    : A â†’ âˆ¥ A âˆ¥[1+ n ]
  hub    : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) â†’ âˆ¥ A âˆ¥[1+ n ]
  spokeâ€² : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) (x : ğ•Š n) â†’ r x P.â‰¡ hub r

-- Spoke equalities.

spoke : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) (x : ğ•Š n) â†’ r x â‰¡ hub r
spoke r x = _â†”_.from â‰¡â†”â‰¡ (spokeâ€² r x)

-- The truncation operator produces types of the right h-level.

truncation-has-correct-h-level : âˆ€ n â†’ H-level (1 + n) âˆ¥ A âˆ¥[1+ n ]
truncation-has-correct-h-level {A = A} n =
  _â†”_.from +â†”âˆ€contractibleğ•Šâ†’á´® c
  where
  c : âˆ€ x â†’ Contractible ((ğ•Š n , north) â†’á´® (âˆ¥ A âˆ¥[1+ n ] , x))
  c x =
      (const x , (const x (north {A = ğ•Š n})  â‰¡âŸ¨âŸ©
                  x                          âˆ))
    , Î» { (f , fnâ‰¡x) â†’ Î£-â‰¡,â‰¡â†’â‰¡
            (âŸ¨extâŸ© Î» y â†’
               const x y  â‰¡âŸ¨âŸ©
               x          â‰¡âŸ¨ sym fnâ‰¡x âŸ©
               f north    â‰¡âŸ¨ spoke f north âŸ©
               hub f      â‰¡âŸ¨ sym $ spoke f y âŸ©âˆ
               f y        âˆ)
            (subst (Î» f â†’ f north â‰¡ x)
                   (âŸ¨extâŸ© (Î» y â†’ trans (sym fnâ‰¡x)
                                   (trans (spoke f north)
                                      (sym (spoke f y)))))
                   (refl x)                                             â‰¡âŸ¨ subst-ext _ _ âŸ©

             subst (_â‰¡ x)
                   (trans (sym fnâ‰¡x)
                      (trans (spoke f north) (sym (spoke f north))))
                   (refl x)                                             â‰¡âŸ¨ cong (Î» p â†’ subst (_â‰¡ x) (trans (sym fnâ‰¡x) p) (refl x)) $ trans-symÊ³ _ âŸ©

             subst (_â‰¡ x) (trans (sym fnâ‰¡x) (refl (f north))) (refl x)  â‰¡âŸ¨ cong (Î» p â†’ subst (_â‰¡ x) p (refl x)) $ trans-reflÊ³ _ âŸ©

             subst (_â‰¡ x) (sym fnâ‰¡x) (refl x)                           â‰¡âŸ¨ subst-trans _ âŸ©

             trans fnâ‰¡x (refl x)                                        â‰¡âŸ¨ trans-reflÊ³ _ âŸ©âˆ

             fnâ‰¡x                                                       âˆ)
        }

-- A dependent eliminator.

module Elimâ€²
  (P : âˆ¥ A âˆ¥[1+ n ] â†’ Set p)
  (f : âˆ€ x â†’ P âˆ£ x âˆ£)
  (h : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) â†’
       (âˆ€ x â†’ P (r x)) â†’
       P (hub r))
  (s : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ])
       (p : âˆ€ x â†’ P (r x))
       (x : ğ•Š n) â†’
       subst P (spoke r x) (p x) â‰¡ h r p)
  where

  elimâ€² : âˆ€ x â†’ P x
  elimâ€² âˆ£ x âˆ£          = f x
  elimâ€² (hub r)        = h r (Î» x â†’ elimâ€² (r x))
  elimâ€² (spokeâ€² r x i) = substâ‰¡â†’[]â‰¡ (s r (Î» x â†’ elimâ€² (r x)) x) i

  elimâ€²-spoke : dcong elimâ€² (spoke r x) â‰¡ s r (Î» x â†’ elimâ€² (r x)) x
  elimâ€²-spoke = dcong-substâ‰¡â†’[]â‰¡ (refl _)

open Elimâ€² public

-- A non-dependent eliminator.

module Recâ€²
  (f : A â†’ B)
  (h : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) â†’ (ğ•Š n â†’ B) â†’ B)
  (s : (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) (p : ğ•Š n â†’ B) (x : ğ•Š n) â†’ p x â‰¡ h r p)
  where

  private
    module E = Elimâ€² (const B) f h
      (Î» r p x â†’
        subst (Î» _ â†’ B) (spoke r x) (p x)  â‰¡âŸ¨ subst-const _ âŸ©
        p x                                â‰¡âŸ¨ s r p x âŸ©âˆ
        h r p                              âˆ)

  recâ€² : âˆ¥ A âˆ¥[1+ n ] â†’ B
  recâ€² = E.elimâ€²

  recâ€²-spoke : cong recâ€² (spoke r x) â‰¡ s r (Î» x â†’ recâ€² (r x)) x
  recâ€²-spoke = dcongâ‰¡â†’congâ‰¡ E.elimâ€²-spoke

open Recâ€² public

-- A dependent eliminator that can be used when the motive is a family
-- of types, all of a certain h-level.

elim :
  (P : âˆ¥ A âˆ¥[1+ n ] â†’ Set p) â†’
  (âˆ€ x â†’ H-level (1 + n) (P x)) â†’
  (âˆ€ x â†’ P âˆ£ x âˆ£) â†’
  âˆ€ x â†’ P x
elim {A = A} {n = n} P P-h f = elimâ€² P f h s
  where

  module _ (r : ğ•Š n â†’ âˆ¥ A âˆ¥[1+ n ]) (p : âˆ€ x â†’ P (r x)) where

    hâ€² : ğ•Š n â†’ P (hub r)
    hâ€² x = subst P (spoke r x) (p x)

    h = hâ€² north

    lemma =                                                    $âŸ¨ P-h âŸ©
      (âˆ€ x â†’ H-level (1 + n) (P x))                            â†âŸ¨ _$ _ âŸ©
      H-level (1 + n) (P (hub r))                              â†”âŸ¨ +â†”âˆ€contractibleğ•Šâ†’á´® âŸ©
      (âˆ€ h â†’ Contractible ((ğ•Š n , north) â†’á´® (P (hub r) , h)))  â†âŸ¨ _$ _ âŸ©
      Contractible ((ğ•Š n , north) â†’á´® (P (hub r) , h))          â†âŸ¨ monoâ‚ _ âŸ©â–¡
      Is-proposition ((ğ•Š n , north) â†’á´® (P (hub r) , h))        â–¡

    s = Î» x â†’
      subst P (spoke r x) (p x)  â‰¡âŸ¨âŸ©
      hâ€² x                       â‰¡âŸ¨ cong (Î» f â†’ projâ‚ f x) $ lemma (hâ€² , refl _) (const h , refl _) âŸ©
      const h x                  â‰¡âŸ¨âŸ©
      h                          âˆ

-- A non-dependent eliminator that can be used when the motive is a
-- type of a certain h-level.

rec : H-level (1 + n) B â†’ (A â†’ B) â†’ âˆ¥ A âˆ¥[1+ n ] â†’ B
rec B-h = elim _ (const B-h)

-- Dependent functions into P that agree on the image of âˆ£_âˆ£ agree
-- everywhere, if P is a family of types that all have a certain
-- h-level.

uniquenessâ€² :
  {f g : (x : âˆ¥ A âˆ¥[1+ n ]) â†’ P x} â†’
  (âˆ€ x â†’ H-level (2 + n) (P x)) â†’
  ((x : A) â†’ f âˆ£ x âˆ£ â‰¡ g âˆ£ x âˆ£) â†’
  ((x : âˆ¥ A âˆ¥[1+ n ]) â†’ f x â‰¡ g x)
uniquenessâ€² {n = n} P-h = elim _ (Î» _ â†’ +â‡’â‰¡ {n = suc n} (P-h _))

-- A special case of the previous property.

uniqueness :
  {f g : âˆ¥ A âˆ¥[1+ n ] â†’ B} â†’
  H-level (1 + n) B â†’
  ((x : A) â†’ f âˆ£ x âˆ£ â‰¡ g âˆ£ x âˆ£) â†’
  ((x : âˆ¥ A âˆ¥[1+ n ]) â†’ f x â‰¡ g x)
uniqueness h = uniquenessâ€² (Î» _ â†’ monoâ‚ _ h)

-- The truncation operator's universal property.

universal-property :
  H-level (1 + n) B â†’
  (âˆ¥ A âˆ¥[1+ n ] â†’ B) â†” (A â†’ B)
universal-property h = record
  { surjection = record
    { logical-equivalence = record
      { to   = _âˆ˜ âˆ£_âˆ£
      ; from = rec h
      }
    ; right-inverse-of = refl
    }
  ; left-inverse-of = Î» f â†’ âŸ¨extâŸ© $ uniqueness h (Î» x â†’ f âˆ£ x âˆ£  âˆ)
  }

-- The truncation operator âˆ¥_âˆ¥[1+Â nÂ ] is a functor.

âˆ¥âˆ¥-map : (A â†’ B) â†’ âˆ¥ A âˆ¥[1+ n ] â†’ âˆ¥ B âˆ¥[1+ n ]
âˆ¥âˆ¥-map {A = A} {B = B} {n = n} =
  (A â†’ B)                        â†âŸ¨ âˆ£_âˆ£ âˆ˜_ âŸ©
  (A â†’ âˆ¥ B âˆ¥[1+ n ])             â†”âŸ¨ inverse $ universal-property (truncation-has-correct-h-level _) âŸ©â–¡
  (âˆ¥ A âˆ¥[1+ n ] â†’ âˆ¥ B âˆ¥[1+ n ])  â–¡

âˆ¥âˆ¥-map-id :
  (x : âˆ¥ A âˆ¥[1+ n ]) â†’
  âˆ¥âˆ¥-map id x â‰¡ x
âˆ¥âˆ¥-map-id = uniqueness
  (truncation-has-correct-h-level _)
  (Î» x â†’ âˆ£ x âˆ£  âˆ)

âˆ¥âˆ¥-map-âˆ˜ :
  (x : âˆ¥ A âˆ¥[1+ n ]) â†’
  âˆ¥âˆ¥-map (f âˆ˜ g) x â‰¡ âˆ¥âˆ¥-map f (âˆ¥âˆ¥-map g x)
âˆ¥âˆ¥-map-âˆ˜ {f = f} {g = g} = uniqueness
  (truncation-has-correct-h-level _)
  (Î» x â†’ âˆ£ f (g x) âˆ£  âˆ)

-- A has h-level 1Â +Â n if and only if it is isomorphic to
-- âˆ¥Â AÂ âˆ¥[1+Â nÂ ].

+â‡”âˆ¥âˆ¥â†” : H-level (1 + n) A â‡” (âˆ¥ A âˆ¥[1+ n ] â†” A)
+â‡”âˆ¥âˆ¥â†” {n = n} {A = A} = record
  { to = Î» h â†’ record
    { surjection = record
      { logical-equivalence = record
        { to   = rec h id
        ; from = âˆ£_âˆ£
        }
      ; right-inverse-of = refl
      }
    ; left-inverse-of = elim _
        (Î» _ â†’ â‡’â‰¡ _ $ truncation-has-correct-h-level _)
        (Î» x â†’ âˆ£ x âˆ£  âˆ)
    }
  ; from =
      âˆ¥ A âˆ¥[1+ n ] â†” A                                    â†âŸ¨ H-level-cong ext _ âŸ©
      (H-level (1 + n) âˆ¥ A âˆ¥[1+ n ] â†” H-level (1 + n) A)  â†âŸ¨ (Î» hyp â†’ _â†”_.to hyp (truncation-has-correct-h-level _)) âŸ©â–¡
      H-level (1 + n) A                                   â–¡
  }

-- Nested truncations where the inner truncation's h-level is at least
-- as large as the outer truncation's h-level can be flattened.

flatten-â‰¥ : m â‰¤ n â†’ âˆ¥ âˆ¥ A âˆ¥[1+ n ] âˆ¥[1+ m ] â†” âˆ¥ A âˆ¥[1+ m ]
flatten-â‰¥ mâ‰¤n = record
  { surjection = record
    { logical-equivalence = record
      { to   = rec (truncation-has-correct-h-level _)
                   (rec (mono (Nat.sucâ‰¤suc mâ‰¤n)
                              (truncation-has-correct-h-level _))
                        âˆ£_âˆ£)
      ; from = âˆ¥âˆ¥-map âˆ£_âˆ£
      }
    ; right-inverse-of = uniqueness
        (truncation-has-correct-h-level _)
        (Î» x â†’ âˆ£ x âˆ£  âˆ)
    }
  ; left-inverse-of = uniqueness
      (truncation-has-correct-h-level _)
      (uniqueness
         (mono (Nat.sucâ‰¤suc mâ‰¤n)
               (truncation-has-correct-h-level _))
         (Î» x â†’ âˆ£ âˆ£ x âˆ£ âˆ£  âˆ))
  }

-- The remainder of this module is not based on the HoTT book.

-- Nested truncations where the inner truncation's h-level is at most
-- as large as the outer truncation's h-level can be flattened.

flatten-â‰¤ : m â‰¤ n â†’ âˆ¥ âˆ¥ A âˆ¥[1+ m ] âˆ¥[1+ n ] â†” âˆ¥ A âˆ¥[1+ m ]
flatten-â‰¤ mâ‰¤n = record
  { surjection = record
    { logical-equivalence = record
      { to   = rec (mono (Nat.sucâ‰¤suc mâ‰¤n)
                         (truncation-has-correct-h-level _))
                   id
      ; from = âˆ£_âˆ£
      }
    ; right-inverse-of = refl
    }
  ; left-inverse-of = uniqueness
      (truncation-has-correct-h-level _)
      (Î» x â†’ âˆ£ x âˆ£  âˆ)
  }

-- Nested truncations can be flattened.

flatten : âˆ¥ âˆ¥ A âˆ¥[1+ m ] âˆ¥[1+ n ] â†” âˆ¥ A âˆ¥[1+ min m n ]
flatten {A = A} {m = m} {n = n} = case Nat.total m n of Î» where
  (injâ‚ mâ‰¤n) â†’ âˆ¥ âˆ¥ A âˆ¥[1+ m ] âˆ¥[1+ n ]  â†âŸ¨ flatten-â‰¤ mâ‰¤n âŸ©
               âˆ¥ A âˆ¥[1+ m ]             â†âŸ¨ â‰¡â‡’â† _ $ cong âˆ¥ A âˆ¥[1+_] $ sym $ _â‡”_.to Nat.â‰¤â‡”minâ‰¡ mâ‰¤n âŸ©â–¡
               âˆ¥ A âˆ¥[1+ min m n ]       â–¡
  (injâ‚‚ mâ‰¥n) â†’ âˆ¥ âˆ¥ A âˆ¥[1+ m ] âˆ¥[1+ n ]  â†âŸ¨ flatten-â‰¥ mâ‰¥n âŸ©
               âˆ¥ A âˆ¥[1+ n ]             â†âŸ¨ â‰¡â‡’â† _ $ cong âˆ¥ A âˆ¥[1+_] $ sym $ _â‡”_.to Nat.â‰¤â‡”minâ‰¡ mâ‰¥n âŸ©
               âˆ¥ A âˆ¥[1+ min n m ]       â†âŸ¨ â‰¡â‡’â† _ $ cong âˆ¥ A âˆ¥[1+_] $ Nat.min-comm _ _ âŸ©â–¡
               âˆ¥ A âˆ¥[1+ min m n ]       â–¡

-- The propositional truncation operator âˆ¥_âˆ¥ is pointwise isomorphic
-- to âˆ¥_âˆ¥[1+Â 0Â ].

âˆ¥âˆ¥â†”âˆ¥âˆ¥ : âˆ¥ A âˆ¥ â†” âˆ¥ A âˆ¥[1+ 0 ]
âˆ¥âˆ¥â†”âˆ¥âˆ¥ = record
  { surjection = record
    { logical-equivalence = record
      { to   = TP.rec (truncation-has-correct-h-level 0) âˆ£_âˆ£
      ; from = rec TP.truncation-is-proposition TP.âˆ£_âˆ£
      }
    ; right-inverse-of = Î» _ â†’ truncation-has-correct-h-level 0 _ _
    }
  ; left-inverse-of = Î» _ â†’ TP.truncation-is-proposition _ _
  }

-- A universe-polymorphic variant of bind.

infixl 5 _>>=â€²_

_>>=â€²_ : âˆ¥ A âˆ¥[1+ n ] â†’ (A â†’ âˆ¥ B âˆ¥[1+ n ]) â†’ âˆ¥ B âˆ¥[1+ n ]
_>>=â€²_ {A = A} {n = n} {B = B} = curry (
  âˆ¥ A âˆ¥[1+ n ] Ã— (A â†’ âˆ¥ B âˆ¥[1+ n ])  â†âŸ¨ uncurry (flip âˆ¥âˆ¥-map) âŸ©
  âˆ¥ âˆ¥ B âˆ¥[1+ n ] âˆ¥[1+ n ]            â†”âŸ¨ flatten-â‰¤ Nat.â‰¤-refl âŸ©â–¡
  âˆ¥ B âˆ¥[1+ n ]                       â–¡)

-- âˆ¥_âˆ¥[1+Â nÂ ] is a monad.

instance

  monad : Monad {c = â„“} (âˆ¥_âˆ¥[1+ n ])
  Raw-monad.return (Monad.raw-monad monad) = âˆ£_âˆ£

  Raw-monad._>>=_ (Monad.raw-monad monad) = _>>=â€²_

  Monad.left-identity monad = Î» _ _ â†’ refl _

  Monad.right-identity monad =
    uniqueness (truncation-has-correct-h-level _) (Î» _ â†’ refl _)

  Monad.associativity monad = flip Î» f â†’ flip Î» g â†’ uniqueness
    (truncation-has-correct-h-level _)
    (Î» x â†’ f x >>=â€² g  âˆ)

-- The truncation operator preserves logical equivalences.

âˆ¥âˆ¥-cong-â‡” : A â‡” B â†’ âˆ¥ A âˆ¥[1+ n ] â‡” âˆ¥ B âˆ¥[1+ n ]
âˆ¥âˆ¥-cong-â‡” Aâ‡”B = record
  { to   = âˆ¥âˆ¥-map (_â‡”_.to   Aâ‡”B)
  ; from = âˆ¥âˆ¥-map (_â‡”_.from Aâ‡”B)
  }

-- The truncation operator preserves bijections.

âˆ¥âˆ¥-cong : A â†”[ k ] B â†’ âˆ¥ A âˆ¥[1+ n ] â†”[ k ] âˆ¥ B âˆ¥[1+ n ]
âˆ¥âˆ¥-cong {n = n} Aâ†B = from-bijection (record
  { surjection = record
    { logical-equivalence = record
      { to   = âˆ¥âˆ¥-map (_â†”_.to   Aâ†”B)
      ; from = âˆ¥âˆ¥-map (_â†”_.from Aâ†”B)
      }
    ; right-inverse-of = lemma Aâ†”B
    }
  ; left-inverse-of = lemma (inverse Aâ†”B)
  })
  where
  Aâ†”B = from-isomorphism Aâ†B

  lemma :
    (Aâ†”B : A â†” B) (x : âˆ¥ B âˆ¥[1+ n ]) â†’
    âˆ¥âˆ¥-map (_â†”_.to Aâ†”B) (âˆ¥âˆ¥-map (_â†”_.from Aâ†”B) x) â‰¡ x
  lemma Aâ†”B x =
    âˆ¥âˆ¥-map (_â†”_.to Aâ†”B) (âˆ¥âˆ¥-map (_â†”_.from Aâ†”B) x)  â‰¡âŸ¨ sym $ âˆ¥âˆ¥-map-âˆ˜ x âŸ©
    âˆ¥âˆ¥-map (_â†”_.to Aâ†”B âˆ˜ _â†”_.from Aâ†”B) x           â‰¡âŸ¨ cong (Î» f â†’ âˆ¥âˆ¥-map f x) $ âŸ¨extâŸ© $ _â†”_.right-inverse-of Aâ†”B âŸ©
    âˆ¥âˆ¥-map id x                                    â‰¡âŸ¨ âˆ¥âˆ¥-map-id x âŸ©âˆ
    x                                              âˆ

-- âˆ¥Â AÂ âˆ¥[1+_] is downwards closed.

downwards-closed : m â‰¤ n â†’ âˆ¥ A âˆ¥[1+ n ] â†’ âˆ¥ A âˆ¥[1+ m ]
downwards-closed {m = m} {n = n} {A = A} mâ‰¤n =
  âˆ¥ A âˆ¥[1+ n ]             â†âŸ¨ âˆ¥âˆ¥-map âˆ£_âˆ£ âŸ©
  âˆ¥ âˆ¥ A âˆ¥[1+ m ] âˆ¥[1+ n ]  â†”âŸ¨ flatten-â‰¤ mâ‰¤n âŸ©â–¡
  âˆ¥ A âˆ¥[1+ m ]             â–¡
