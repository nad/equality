------------------------------------------------------------------------
-- The one-step truncation
------------------------------------------------------------------------

{-# OPTIONS --cubical --safe #-}

-- The module is parametrised by a notion of equality. The higher
-- constructor of the HIT defining the one-step truncation operator
-- uses path equality, but the supplied notion of equality is used for
-- many other things.

import Equality.Path as P

module H-level.Truncation.Propositional.One-step
  {eâº} (eq : âˆ€ {a p} â†’ P.Equality-with-paths a p eâº) where

open P.Derived-definitions-and-properties eq hiding (elim)

open import Logical-equivalence using (_â‡”_)
open import Prelude

open import Bijection equality-with-J as B using (_â†”_)
open import Circle eq as C using (ğ•ŠÂ¹)
open import Equality.Decidable-UIP equality-with-J using (Constant)
import Equality.Decidable-UIP P.equality-with-J as PD
open import Equality.Path.Isomorphisms eq
open import Equivalence equality-with-J as Eq using (_â‰ƒ_)
import Equivalence P.equality-with-J as PE
open import Equivalence.Erased.Basics equality-with-J as EEq
  using (_â‰ƒá´±_)
open import Function-universe equality-with-J as F hiding (id; _âˆ˜_)
import H-level equality-with-J as H-level
open import H-level.Closure equality-with-J
open import Surjection equality-with-J using (_â† _)

private
  variable
    a b p   : Level
    A B     : Type a
    P       : A â†’ Type p
    e r x y : A
    n       : â„•

------------------------------------------------------------------------
-- The type

-- One-step truncation.
--
-- This definition is based on that in van Doorn's "Constructing the
-- Propositional Truncation using Non-recursive HITs".

data âˆ¥_âˆ¥Â¹ (A : Type a) : Type a where
  âˆ£_âˆ£          : A â†’ âˆ¥ A âˆ¥Â¹
  âˆ£âˆ£-constantá´¾ : PD.Constant âˆ£_âˆ£

-- The function âˆ£_âˆ£ is constant.

âˆ£âˆ£-constant : Constant (âˆ£_âˆ£ {A = A})
âˆ£âˆ£-constant x y = _â†”_.from â‰¡â†”â‰¡ (âˆ£âˆ£-constantá´¾ x y)

------------------------------------------------------------------------
-- Eliminators

-- A dependent eliminator, expressed using paths.

record Elimá´¾ {A : Type a} (P : âˆ¥ A âˆ¥Â¹ â†’ Type p) : Type (a âŠ” p) where
  no-eta-equality
  field
    âˆ£âˆ£Ê³          : âˆ€ x â†’ P âˆ£ x âˆ£
    âˆ£âˆ£-constantÊ³ :
      (x y : A) â†’
      P.[ (Î» i â†’ P (âˆ£âˆ£-constantá´¾ x y i)) ] âˆ£âˆ£Ê³ x â‰¡ âˆ£âˆ£Ê³ y

open Elimá´¾ public

elimá´¾ : Elimá´¾ P â†’ (x : âˆ¥ A âˆ¥Â¹) â†’ P x
elimá´¾ {A = A} {P = P} e = helper
  where
  module E = Elimá´¾ e

  helper : (x : âˆ¥ A âˆ¥Â¹) â†’ P x
  helper âˆ£ x âˆ£                = E.âˆ£âˆ£Ê³ x
  helper (âˆ£âˆ£-constantá´¾ x y i) = E.âˆ£âˆ£-constantÊ³ x y i

-- A non-dependent eliminator, expressed using paths.

record Recá´¾ (A : Type a) (B : Type b) : Type (a âŠ” b) where
  no-eta-equality
  field
    âˆ£âˆ£Ê³          : A â†’ B
    âˆ£âˆ£-constantÊ³ : PD.Constant âˆ£âˆ£Ê³

open Recá´¾ public

recá´¾ : Recá´¾ A B â†’ âˆ¥ A âˆ¥Â¹ â†’ B
recá´¾ r = elimá´¾ Î» where
    .âˆ£âˆ£Ê³          â†’ R.âˆ£âˆ£Ê³
    .âˆ£âˆ£-constantÊ³ â†’ R.âˆ£âˆ£-constantÊ³
  where
  module R = Recá´¾ r

-- A dependent eliminator.

record Elim {A : Type a} (P : âˆ¥ A âˆ¥Â¹ â†’ Type p) : Type (a âŠ” p) where
  no-eta-equality
  field
    âˆ£âˆ£Ê³          : âˆ€ x â†’ P âˆ£ x âˆ£
    âˆ£âˆ£-constantÊ³ :
      (x y : A) â†’ subst P (âˆ£âˆ£-constant x y) (âˆ£âˆ£Ê³ x) â‰¡ âˆ£âˆ£Ê³ y

open Elim public

elim : Elim P â†’ (x : âˆ¥ A âˆ¥Â¹) â†’ P x
elim e = elimá´¾ Î» where
    .âˆ£âˆ£Ê³              â†’ E.âˆ£âˆ£Ê³
    .âˆ£âˆ£-constantÊ³ x y â†’ substâ‰¡â†’[]â‰¡ (E.âˆ£âˆ£-constantÊ³ x y)
  where
  module E = Elim e

-- A "computation" rule.

elim-âˆ£âˆ£-constant :
  dcong (elim e) (âˆ£âˆ£-constant x y) â‰¡ Elim.âˆ£âˆ£-constantÊ³ e x y
elim-âˆ£âˆ£-constant = dcong-substâ‰¡â†’[]â‰¡ (refl _)

-- A non-dependent eliminator.

record Rec (A : Type a) (B : Type b) : Type (a âŠ” b) where
  no-eta-equality
  field
    âˆ£âˆ£Ê³          : A â†’ B
    âˆ£âˆ£-constantÊ³ : Constant âˆ£âˆ£Ê³

open Rec public

rec : Rec A B â†’ âˆ¥ A âˆ¥Â¹ â†’ B
rec r = recá´¾ Î» where
    .âˆ£âˆ£Ê³              â†’ R.âˆ£âˆ£Ê³
    .âˆ£âˆ£-constantÊ³ x y â†’ _â†”_.to â‰¡â†”â‰¡ (R.âˆ£âˆ£-constantÊ³ x y)
  where
  module R = Rec r

-- A "computation" rule.

rec-âˆ£âˆ£-constant :
  cong (rec r) (âˆ£âˆ£-constant x y) â‰¡ Rec.âˆ£âˆ£-constantÊ³ r x y
rec-âˆ£âˆ£-constant = cong-â‰¡â†”â‰¡ (refl _)

-- A variant of rec.

recâ€² : (f : A â†’ B) â†’ Constant f â†’ âˆ¥ A âˆ¥Â¹ â†’ B
recâ€² f c = rec Î» where
  .Rec.âˆ£âˆ£Ê³          â†’ f
  .Rec.âˆ£âˆ£-constantÊ³ â†’ c

------------------------------------------------------------------------
-- Some preservation lemmas

-- A map function for âˆ¥_âˆ¥Â¹.

âˆ¥âˆ¥Â¹-map : (A â†’ B) â†’ âˆ¥ A âˆ¥Â¹ â†’ âˆ¥ B âˆ¥Â¹
âˆ¥âˆ¥Â¹-map f = rec Î» where
  .âˆ£âˆ£Ê³ x            â†’ âˆ£ f x âˆ£
  .âˆ£âˆ£-constantÊ³ x y â†’ âˆ£âˆ£-constant (f x) (f y)

-- The truncation operator preserves logical equivalences.

âˆ¥âˆ¥Â¹-cong-â‡” : A â‡” B â†’ âˆ¥ A âˆ¥Â¹ â‡” âˆ¥ B âˆ¥Â¹
âˆ¥âˆ¥Â¹-cong-â‡” Aâ‡”B = record
  { to   = âˆ¥âˆ¥Â¹-map (_â‡”_.to   Aâ‡”B)
  ; from = âˆ¥âˆ¥Â¹-map (_â‡”_.from Aâ‡”B)
  }

private

  -- A lemma used below.

  âˆ¥âˆ¥Â¹-cong-lemma :
    (f : A â†’ B) (g : B â†’ A) (eq : âˆ€ x â†’ f (g x) â‰¡ x) â†’
    subst (Î» z â†’ âˆ¥âˆ¥Â¹-map f (âˆ¥âˆ¥Â¹-map g z) â‰¡ z)
      (âˆ£âˆ£-constant x y) (cong âˆ£_âˆ£ (eq x)) â‰¡
    cong âˆ£_âˆ£ (eq y)
  âˆ¥âˆ¥Â¹-cong-lemma {x = x} {y = y} f g eq =
    subst
      (Î» z â†’ âˆ¥âˆ¥Â¹-map f (âˆ¥âˆ¥Â¹-map g z) â‰¡ z)
      (âˆ£âˆ£-constant x y) (cong âˆ£_âˆ£ (eq x))                         â‰¡âŸ¨ subst-in-terms-of-trans-and-cong âŸ©

    trans (sym (cong (âˆ¥âˆ¥Â¹-map f âˆ˜ âˆ¥âˆ¥Â¹-map g) (âˆ£âˆ£-constant x y)))
      (trans (cong âˆ£_âˆ£ (eq x)) (cong id (âˆ£âˆ£-constant x y)))       â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym p) (trans (cong âˆ£_âˆ£ (eq x)) q))
                                                                       (trans (sym $ cong-âˆ˜ _ _ _) $
                                                                        trans (cong (cong (âˆ¥âˆ¥Â¹-map f)) rec-âˆ£âˆ£-constant) $
                                                                        rec-âˆ£âˆ£-constant)
                                                                       (sym $ cong-id _) âŸ©
    trans (sym (âˆ£âˆ£-constant (f (g x)) (f (g y))))
      (trans (cong âˆ£_âˆ£ (eq x)) (âˆ£âˆ£-constant x y))                 â‰¡âŸ¨ cong (Î» f â†’ trans _ (trans (cong âˆ£_âˆ£ (f _)) _)) $ sym $
                                                                     _â‰ƒ_.left-inverse-of (Eq.extensionality-isomorphism bad-ext) eq âŸ©
    trans (sym (âˆ£âˆ£-constant (f (g x)) (f (g y))))
      (trans (cong âˆ£_âˆ£ (extâ»Â¹ (âŸ¨extâŸ© eq) x)) (âˆ£âˆ£-constant x y))   â‰¡âŸ¨ elimâ‚
                                                                       (Î» {f} eq â†’ trans (sym (âˆ£âˆ£-constant (f x) (f y)))
                                                                                     (trans (cong âˆ£_âˆ£ (extâ»Â¹ eq x)) (âˆ£âˆ£-constant x y)) â‰¡
                                                                                   cong âˆ£_âˆ£ (extâ»Â¹ eq y))
                                                                       (
      trans (sym (âˆ£âˆ£-constant x y))
        (trans (cong âˆ£_âˆ£ (extâ»Â¹ (refl id) x)) (âˆ£âˆ£-constant x y))        â‰¡âŸ¨ cong (trans _) $
                                                                           trans (cong (flip trans _) $
                                                                                  trans (cong (cong _) $ cong-refl _) $
                                                                                  cong-refl _) $
                                                                           trans-reflË¡ _ âŸ©

      trans (sym (âˆ£âˆ£-constant x y)) (âˆ£âˆ£-constant x y)                   â‰¡âŸ¨ trans-symË¡ _ âŸ©

      refl _                                                            â‰¡âŸ¨ trans (sym $ cong-refl _) $
                                                                           cong (cong _) $ sym $ cong-refl _ âŸ©âˆ
      cong âˆ£_âˆ£ (extâ»Â¹ (refl _) y)                                       âˆ)
                                                                       _ âŸ©

    cong âˆ£_âˆ£ (extâ»Â¹ (âŸ¨extâŸ© eq) y)                                 â‰¡âŸ¨ cong (Î» f â†’ cong âˆ£_âˆ£ (f _)) $
                                                                     _â‰ƒ_.left-inverse-of (Eq.extensionality-isomorphism bad-ext) eq âŸ©âˆ
    cong âˆ£_âˆ£ (eq y)                                               âˆ

-- The truncation operator preserves split surjections.

âˆ¥âˆ¥Â¹-cong-â†  : A â†  B â†’ âˆ¥ A âˆ¥Â¹ â†  âˆ¥ B âˆ¥Â¹
âˆ¥âˆ¥Â¹-cong-â†  Aâ† B = record
  { logical-equivalence = âˆ¥âˆ¥Â¹-cong-â‡” (_â† _.logical-equivalence Aâ† B)
  ; right-inverse-of    = elim Î» where
      .âˆ£âˆ£Ê³ x            â†’ cong âˆ£_âˆ£ (_â† _.right-inverse-of Aâ† B x)
      .âˆ£âˆ£-constantÊ³ x y â†’
        âˆ¥âˆ¥Â¹-cong-lemma (_â† _.to Aâ† B) (_â† _.from Aâ† B)
          (_â† _.right-inverse-of Aâ† B)
  }

-- The truncation operator preserves bijections.

âˆ¥âˆ¥Â¹-cong-â†” : A â†” B â†’ âˆ¥ A âˆ¥Â¹ â†” âˆ¥ B âˆ¥Â¹
âˆ¥âˆ¥Â¹-cong-â†” Aâ†”B = record
  { surjection      = âˆ¥âˆ¥Â¹-cong-â†  (_â†”_.surjection Aâ†”B)
  ; left-inverse-of = elim Î» where
      .âˆ£âˆ£Ê³ x            â†’ cong âˆ£_âˆ£ (_â†”_.left-inverse-of Aâ†”B x)
      .âˆ£âˆ£-constantÊ³ x y â†’
        âˆ¥âˆ¥Â¹-cong-lemma (_â†”_.from Aâ†”B) (_â†”_.to Aâ†”B)
          (_â†”_.left-inverse-of Aâ†”B)
  }

-- The truncation operator preserves equivalences.

âˆ¥âˆ¥Â¹-cong-â‰ƒ : A â‰ƒ B â†’ âˆ¥ A âˆ¥Â¹ â‰ƒ âˆ¥ B âˆ¥Â¹
âˆ¥âˆ¥Â¹-cong-â‰ƒ = from-isomorphism âˆ˜ âˆ¥âˆ¥Â¹-cong-â†” âˆ˜ from-isomorphism

-- The truncation operator preserves equivalences with erased proofs.

âˆ¥âˆ¥Â¹-cong-â‰ƒá´± : A â‰ƒá´± B â†’ âˆ¥ A âˆ¥Â¹ â‰ƒá´± âˆ¥ B âˆ¥Â¹
âˆ¥âˆ¥Â¹-cong-â‰ƒá´± Aâ‰ƒB = EEq.[â‰ƒ]â†’â‰ƒá´± (EEq.[proofs] (âˆ¥âˆ¥Â¹-cong-â‰ƒ (EEq.â‰ƒá´±â†’â‰ƒ Aâ‰ƒB)))

------------------------------------------------------------------------
-- A result related to h-levels

-- The one-step truncation of the unit type is equivalent to the
-- circle.
--
-- Paolo Capriotti informed me about this result.

âˆ¥âŠ¤âˆ¥Â¹â‰ƒğ•ŠÂ¹ : âˆ¥ âŠ¤ âˆ¥Â¹ â‰ƒ ğ•ŠÂ¹
âˆ¥âŠ¤âˆ¥Â¹â‰ƒğ•ŠÂ¹ = _â†”_.from â‰ƒâ†”â‰ƒ $ PE.â†”â†’â‰ƒ
  (recá´¾ Î» where
     .âˆ£âˆ£Ê³ _            â†’ C.base
     .âˆ£âˆ£-constantÊ³ _ _ â†’ C.loopá´¾)
  (C.recá´¾ âˆ£ _ âˆ£ (âˆ£âˆ£-constantá´¾ _ _))
  (C.elimá´¾ _ P.refl (Î» _ â†’ P.refl))
  (elimá´¾ Î» where
     .âˆ£âˆ£Ê³ _              â†’ P.refl
     .âˆ£âˆ£-constantÊ³ _ _ _ â†’ P.refl)

-- It is not necessarily the case that the one-step truncation of a
-- proposition is a proposition.

Â¬-Is-proposition-âˆ¥âˆ¥Â¹ :
  Â¬ ({A : Type a} â†’ Is-proposition A â†’ Is-proposition âˆ¥ A âˆ¥Â¹)
Â¬-Is-proposition-âˆ¥âˆ¥Â¹ {a = a} =
  ({A : Type a} â†’ Is-proposition A â†’ Is-proposition âˆ¥ A âˆ¥Â¹)  â†âŸ¨ _$ H-level.monoâ‚ 0 (â†‘-closure 0 âŠ¤-contractible) âŸ©
  Is-proposition âˆ¥ â†‘ a âŠ¤ âˆ¥Â¹                                  â†âŸ¨ H-level-cong _ 1 (âˆ¥âˆ¥Â¹-cong-â†” B.â†‘â†”) âŸ©
  Is-proposition âˆ¥ âŠ¤ âˆ¥Â¹                                      â†âŸ¨ H-level-cong _ 1 âˆ¥âŠ¤âˆ¥Â¹â‰ƒğ•ŠÂ¹ âŸ©
  Is-proposition ğ•ŠÂ¹                                          â†âŸ¨ C.Â¬-ğ•ŠÂ¹-set âˆ˜ H-level.monoâ‚ 1 âŸ©â–¡
  âŠ¥                                                          â–¡

------------------------------------------------------------------------
-- Iterated applications of the one-step truncation operator

-- Applies the one-step truncation the given number of times, from the
-- "outside".
--
-- This definition and the next one are taken from van Doorn's
-- "Constructing the Propositional Truncation using Non-recursive
-- HITs".

âˆ¥_âˆ¥Â¹-out-^ : Type a â†’ â„• â†’ Type a
âˆ¥ A âˆ¥Â¹-out-^ zero    = A
âˆ¥ A âˆ¥Â¹-out-^ (suc n) = âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹

-- A "constructor" for âˆ¥_âˆ¥Â¹-out-^.

âˆ£_âˆ£-out-^ : A â†’ âˆ€ n â†’ âˆ¥ A âˆ¥Â¹-out-^ n
âˆ£ x âˆ£-out-^ zero    = x
âˆ£ x âˆ£-out-^ (suc n) = âˆ£ âˆ£ x âˆ£-out-^ n âˆ£

-- A rearrangement lemma.

âˆ¥âˆ¥Â¹-out-^+â‰ƒ : âˆ€ m â†’ âˆ¥ A âˆ¥Â¹-out-^ (m + n) â‰ƒ âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹-out-^ m
âˆ¥âˆ¥Â¹-out-^+â‰ƒ                 zero    = F.id
âˆ¥âˆ¥Â¹-out-^+â‰ƒ {A = A} {n = n} (suc m) =
  âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ (m + n) âˆ¥Â¹         â†âŸ¨ âˆ¥âˆ¥Â¹-cong-â‰ƒ (âˆ¥âˆ¥Â¹-out-^+â‰ƒ m) âŸ©â–¡
  âˆ¥ âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹-out-^ m âˆ¥Â¹  â–¡

-- âˆ¥_âˆ¥Â¹ commutes with âˆ¥_âˆ¥Â¹-out-^Â n.

âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute : âˆ€ n â†’ âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹ â†” âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-out-^ n
âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute         zero    = F.id
âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute {A = A} (suc n) =
  âˆ¥ âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹ âˆ¥Â¹  â†âŸ¨ âˆ¥âˆ¥Â¹-cong-â†” (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute n) âŸ©â–¡
  âˆ¥ âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-out-^ n âˆ¥Â¹  â–¡

private

  -- The lemma above is defined using _â†”_ rather than _â‰ƒ_ because the
  -- following equalities hold by definition when _â†”_ is used, but (at
  -- the time of writing) the second one does not hold when _â‰ƒ_ is
  -- used.

  _ :
    {x : A} â†’
    _â†”_.left-inverse-of (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute 0) âˆ£ x âˆ£ â‰¡ refl âˆ£ x âˆ£
  _ = refl _

  _ :
    {A : Type a} {x : âˆ¥ A âˆ¥Â¹-out-^ (suc n)} â†’
    _â†”_.left-inverse-of (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute (1 + n)) âˆ£ x âˆ£ â‰¡
    cong âˆ£_âˆ£ (_â†”_.left-inverse-of (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute n) x)
  _ = refl _

-- A variant of âˆ¥_âˆ¥Â¹-out-^ which applies the one-step truncation the
-- given number of times from the "inside".

âˆ¥_âˆ¥Â¹-in-^ : Type a â†’ â„• â†’ Type a
âˆ¥ A âˆ¥Â¹-in-^ zero    = A
âˆ¥ A âˆ¥Â¹-in-^ (suc n) = âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-in-^ n

-- The two variants of âˆ¥_âˆ¥Â¹^ are pointwise equivalent.

âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ : âˆ€ n â†’ âˆ¥ A âˆ¥Â¹-out-^ n â‰ƒ âˆ¥ A âˆ¥Â¹-in-^ n
âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^         zero    = F.id
âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ {A = A} (suc n) =
  âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹  â†”âŸ¨ âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute n âŸ©
  âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-out-^ n  â†âŸ¨ âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ n âŸ©â–¡
  âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-in-^ n   â–¡

-- âˆ¥_âˆ¥Â¹ commutes with âˆ¥_âˆ¥Â¹-in-^Â n.

âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-in-^-commute : âˆ€ n â†’ âˆ¥ âˆ¥ A âˆ¥Â¹-in-^ n âˆ¥Â¹ â‰ƒ âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-in-^ n
âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-in-^-commute {A = A} n =
  âˆ¥ âˆ¥ A âˆ¥Â¹-in-^ n âˆ¥Â¹    â†âŸ¨ âˆ¥âˆ¥Â¹-cong-â‰ƒ (inverse $ âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ n) âŸ©
  âˆ¥ âˆ¥ A âˆ¥Â¹-out-^ n âˆ¥Â¹   â†”âŸ¨âŸ©
  âˆ¥ A âˆ¥Â¹-out-^ (suc n)  â†âŸ¨ âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (suc n) âŸ©
  âˆ¥ A âˆ¥Â¹-in-^ (suc n)   â†”âŸ¨âŸ©
  âˆ¥ âˆ¥ A âˆ¥Â¹ âˆ¥Â¹-in-^ n    â–¡

-- A "constructor" for âˆ¥_âˆ¥Â¹-in-^.

âˆ£_,_âˆ£-in-^ : âˆ€ n â†’ âˆ¥ A âˆ¥Â¹-in-^ n â†’ âˆ¥ A âˆ¥Â¹-in-^ (suc n)
âˆ£ zero  , x âˆ£-in-^ = âˆ£ x âˆ£
âˆ£ suc n , x âˆ£-in-^ = âˆ£ n , x âˆ£-in-^

-- âˆ£_,_âˆ£-in-^ is related to âˆ£_âˆ£.

âˆ£âˆ£â‰¡âˆ£,âˆ£-in-^ :
  âˆ€ n {x : âˆ¥ A âˆ¥Â¹-out-^ n} â†’
  _â‰ƒ_.to (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (suc n)) âˆ£ x âˆ£ â‰¡
  âˆ£ n , _â‰ƒ_.to (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ n) x âˆ£-in-^
âˆ£âˆ£â‰¡âˆ£,âˆ£-in-^ zero    {x = x} = âˆ£ x âˆ£  âˆ
âˆ£âˆ£â‰¡âˆ£,âˆ£-in-^ (suc n) {x = x} =
  _â‰ƒ_.to (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (2 + n)) âˆ£ x âˆ£            â‰¡âŸ¨âŸ©

  _â‰ƒ_.to (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (1 + n))
    âˆ£ _â†”_.to (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute n) x âˆ£             â‰¡âŸ¨ âˆ£âˆ£â‰¡âˆ£,âˆ£-in-^ n âŸ©âˆ

  âˆ£ n , _â‰ƒ_.to (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ n)
          (_â†”_.to (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute n) x) âˆ£-in-^  âˆ

-- A variant of âˆ£âˆ£â‰¡âˆ£,âˆ£-in-^.

âˆ£,âˆ£-in-^â‰¡âˆ£âˆ£ :
  âˆ€ n {x : âˆ¥ A âˆ¥Â¹-in-^ n} â†’
  _â‰ƒ_.from (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (suc n)) âˆ£ n , x âˆ£-in-^ â‰¡
  âˆ£ _â‰ƒ_.from (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ n) x âˆ£
âˆ£,âˆ£-in-^â‰¡âˆ£âˆ£ zero    {x = x} = âˆ£ x âˆ£  âˆ
âˆ£,âˆ£-in-^â‰¡âˆ£âˆ£ (suc n) {x = x} =
  _â‰ƒ_.from (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (2 + n)) âˆ£ 1 + n , x âˆ£-in-^  â‰¡âŸ¨âŸ©

  _â†”_.from (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute (1 + n))
    (_â‰ƒ_.from (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ (1 + n)) âˆ£ n , x âˆ£-in-^)  â‰¡âŸ¨ cong (_â†”_.from (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute (1 + n))) $ âˆ£,âˆ£-in-^â‰¡âˆ£âˆ£ n âŸ©âˆ

  _â†”_.from (âˆ¥âˆ¥Â¹-âˆ¥âˆ¥Â¹-out-^-commute (1 + n))
    âˆ£ _â‰ƒ_.from (âˆ¥âˆ¥Â¹-out-^â‰ƒâˆ¥âˆ¥Â¹-in-^ n) x âˆ£                   âˆ
