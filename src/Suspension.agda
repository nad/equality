------------------------------------------------------------------------
-- Suspensions
------------------------------------------------------------------------

{-# OPTIONS --cubical --safe #-}

-- The beginning of this module follows the HoTT book rather closely.

-- The module is parametrised by a notion of equality. The higher
-- constructor of the HIT defining suspensions uses path equality, but
-- the supplied notion of equality is used for many other things.

import Equality.Path as P

module Suspension
  {eâº} (eq : âˆ€ {a p} â†’ P.Equality-with-paths a p eâº) where

open P.Derived-definitions-and-properties eq hiding (elim)

open import Logical-equivalence using (_â‡”_)
open import Prelude

open import Bijection equality-with-J as Bijection using (_â†”_)
open import Circle eq as Circle using (ğ•ŠÂ¹; base; loop)
open import Embedding equality-with-J as Embedding using (Embedding)
open import Equality.Decision-procedures equality-with-J
open import Equality.Path.Isomorphisms eq
open import Equality.Tactic equality-with-J
open import Equivalence equality-with-J using (_â‰ƒ_)
open import Function-universe equality-with-J as F hiding (id; _âˆ˜_)
open import H-level equality-with-J
open import H-level.Closure equality-with-J
open import Injection equality-with-J using (_â†£_)
open import Interval eq as Interval using (Interval; [0]; [1]; 0â‰¡1)
import Nat equality-with-J as Nat
open import Pointed-type equality-with-J
open import Surjection equality-with-J using (_â† _)

private
  variable
    a b â„“ â„“â‚ â„“â‚‚ p : Level
    A B           : Type a
    C             : Pointed-type a
    x y           : A
    f g           : A â†’ B
    n             : â„•

-- Suspensions.

data Susp (A : Type a) : Type a where
  north south : Susp A
  meridianá´¾   : A â†’ north P.â‰¡ south

-- Meridians.

meridian : A â†’ _â‰¡_ {A = Susp A} north south
meridian = _â†”_.from â‰¡â†”â‰¡ âˆ˜ meridianá´¾

-- A dependent eliminator, expressed using paths.

elimá´¾ :
  (P : Susp A â†’ Type p)
  (n : P north)
  (s : P south) â†’
  (âˆ€ x â†’ P.[ (Î» i â†’ P (meridianá´¾ x i)) ] n â‰¡ s) â†’
  (x : Susp A) â†’ P x
elimá´¾ _ n s nâ‰¡s = Î» where
  north           â†’ n
  south           â†’ s
  (meridianá´¾ x i) â†’ nâ‰¡s x i

-- A non-dependent eliminator, expressed using paths.

recá´¾ : (n s : B) â†’ (A â†’ n P.â‰¡ s) â†’ Susp A â†’ B
recá´¾ = elimá´¾ _

-- A dependent eliminator.

module Elim
  (P   : Susp A â†’ Type p)
  (n   : P north)
  (s   : P south)
  (nâ‰¡s : âˆ€ x â†’ subst P (meridian x) n â‰¡ s)
  where

  elim : âˆ€ x â†’ P x
  elim = elimá´¾ P n s (substâ‰¡â†’[]â‰¡ âˆ˜ nâ‰¡s)

  -- "Computation" rule for meridians.

  elim-meridian : dcong elim (meridian x) â‰¡ nâ‰¡s x
  elim-meridian = dcong-substâ‰¡â†’[]â‰¡ (refl _)

open Elim public

-- A non-dependent eliminator.

module Rec
  {B : Type b}
  (n s : B)
  (nâ‰¡s : A â†’ n â‰¡ s)
  where

  rec : Susp A â†’ B
  rec = recá´¾ n s (_â†”_.to â‰¡â†”â‰¡ âˆ˜ nâ‰¡s)

  rec-meridian : cong rec (meridian x) â‰¡ nâ‰¡s x
  rec-meridian = cong-â‰¡â†”â‰¡ (refl _)

open Rec public

-- The universal property of suspensions.

universal-property :
  (Susp A â†’ B) â†” (âˆƒ Î» (n : B) â†’ âˆƒ Î» (s : B) â†’ A â†’ n â‰¡ s)
universal-property = record
  { surjection = record
    { logical-equivalence = record
      { to   = Î» f â†’ f north , f south , cong f âˆ˜ meridian
      ; from = Î» { (n , s , f) â†’ rec n s f }
      }
    ; right-inverse-of = Î» { (n , s , f) â†’
        n , s , cong (rec n s f) âˆ˜ meridian  â‰¡âŸ¨ cong (Î» f â†’ n , s , f) $ âŸ¨extâŸ© (Î» _ â†’ rec-meridian n s f) âŸ©âˆ
        n , s , f                            âˆ }
    }
  ; left-inverse-of = Î» f â†’
      let module R = Rec (f north) (f south) (cong f âˆ˜ meridian) in

      R.rec                                                        â‰¡âŸ¨ âŸ¨extâŸ© $ elim _ (refl _) (refl _) (Î» x â†’

          subst (Î» x â†’ R.rec x â‰¡ f x) (meridian x) (refl _)             â‰¡âŸ¨ subst-in-terms-of-trans-and-cong âŸ©

          trans (sym $ cong R.rec (meridian x))
                (trans (refl _) (cong f (meridian x)))                  â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym p) q) R.rec-meridian (trans-reflË¡ _) âŸ©

          trans (sym $ cong f (meridian x)) (cong f (meridian x))       â‰¡âŸ¨ trans-symË¡ _ âŸ©âˆ

          refl _                                                        âˆ) âŸ©âˆ

      f                                                            âˆ
  }

-- Based maps from suspensions of pointed types (using north as the
-- point) are isomorphic to based maps to loop spaces.

Suspâ†’á´®â†” : (Susp A , north) â†’á´® C â†” (A , x) â†’á´® Î© C
Suspâ†’á´®â†” {A = A} {C = B , y} {x = x} =
  (Susp A , north) â†’á´® (B , y)                                            â†”âŸ¨âŸ©
  (âˆƒ Î» (f : Susp A â†’ B) â†’ f north â‰¡ y)                                   â†âŸ¨ Î£-cong universal-property (Î» _ â†’ F.id) âŸ©
  (âˆƒ Î» (f : âˆƒ Î» n â†’ âˆƒ Î» s â†’ A â†’ n â‰¡ s) â†’ projâ‚ f â‰¡ y)                    â†âŸ¨ inverse Î£-assoc âŸ©
  (âˆƒ Î» n â†’ (âˆƒ Î» s â†’ A â†’ n â‰¡ s) Ã— n â‰¡ y)                                  â†âŸ¨ (âˆƒ-cong Î» _ â†’ inverse Î£-assoc) âŸ©
  (âˆƒ Î» n â†’ âˆƒ Î» s â†’ (A â†’ n â‰¡ s) Ã— n â‰¡ y)                                  â†âŸ¨ (âˆƒ-cong Î» _ â†’ âˆƒ-cong Î» _ â†’ Ã—-comm) âŸ©
  (âˆƒ Î» n â†’ âˆƒ Î» s â†’ n â‰¡ y Ã— (A â†’ n â‰¡ s))                                  â†âŸ¨ (âˆƒ-cong Î» _ â†’ âˆƒ-comm) âŸ©
  (âˆƒ Î» n â†’ n â‰¡ y Ã— âˆƒ Î» s â†’ A â†’ n â‰¡ s)                                    â†âŸ¨ Î£-assoc âŸ©
  (âˆƒ Î» (p : âˆƒ Î» n â†’ n â‰¡ y) â†’ âˆƒ Î» s â†’ A â†’ projâ‚ p â‰¡ s)                    â†âŸ¨ drop-âŠ¤-left-Î£ $ _â‡”_.to contractibleâ‡”â†”âŠ¤ $ singleton-contractible _ âŸ©
  (âˆƒ Î» s â†’ A â†’ y â‰¡ s)                                                    â†âŸ¨ (âˆƒ-cong Î» _ â†’ inverse $ drop-âŠ¤-right Î» _ â†’ _â‡”_.to contractibleâ‡”â†”âŠ¤ $
                                                                             other-singleton-contractible _) âŸ©
  (âˆƒ Î» s â†’ âˆƒ Î» (f : A â†’ y â‰¡ s) â†’ âˆƒ Î» (eq : y â‰¡ s) â†’ f x â‰¡ eq)            â†âŸ¨ (âˆƒ-cong Î» _ â†’ âˆƒ-comm) âŸ©
  (âˆƒ Î» s â†’ âˆƒ Î» (eq : y â‰¡ s) â†’ âˆƒ Î» (f : A â†’ y â‰¡ s) â†’ f x â‰¡ eq)            â†âŸ¨ Î£-assoc âŸ©
  (âˆƒ Î» (p : âˆƒ Î» s â†’ y â‰¡ s) â†’ âˆƒ Î» (f : A â†’ y â‰¡ projâ‚ p) â†’ f x â‰¡ projâ‚‚ p)  â†âŸ¨ drop-âŠ¤-left-Î£ $ _â‡”_.to contractibleâ‡”â†”âŠ¤ $
                                                                            other-singleton-contractible _ âŸ©
  (âˆƒ Î» (f : A â†’ y â‰¡ y) â†’ f x â‰¡ refl y)                                   â†”âŸ¨âŸ©
  (A , x) â†’á´® (Î© (B , y))                                                 â–¡

-- The type of booleans can be expressed as a suspension.

Boolâ†”Susp-âŠ¥ : Bool â†” Susp (âŠ¥ {â„“ = â„“})
Boolâ†”Susp-âŠ¥ = record
  { surjection = record
    { logical-equivalence = record
      { to   = if_then north else south
      ; from = rec true false (Î» ())
      }
    ; right-inverse-of = elim _ (refl _) (refl _) (Î» ())
    }
  ; left-inverse-of = Î» where
      true  â†’ refl _
      false â†’ refl _
  }

private

  -- A lemma used in some proofs below.

  subst-in-terms-of-trans-and-congâ€² :
    {xâ‰¡y : x â‰¡ y} {fgxâ‰¡x : f (g x) â‰¡ x} â†’
    subst (Î» z â†’ f (g z) â‰¡ z) xâ‰¡y fgxâ‰¡x â‰¡
    trans (sym (cong f (cong g xâ‰¡y))) (trans fgxâ‰¡x xâ‰¡y)
  subst-in-terms-of-trans-and-congâ€²
    {f = f} {g = g} {xâ‰¡y = xâ‰¡y} {fgxâ‰¡x = fgxâ‰¡x} =

    subst (Î» z â†’ f (g z) â‰¡ z) xâ‰¡y fgxâ‰¡x                         â‰¡âŸ¨ subst-in-terms-of-trans-and-cong âŸ©
    trans (sym (cong (f âˆ˜ g) xâ‰¡y)) (trans fgxâ‰¡x (cong id xâ‰¡y))  â‰¡âŸ¨ sym $ congâ‚‚ (Î» p q â†’ trans (sym p) (trans fgxâ‰¡x q)) (cong-âˆ˜ _ _ _) (cong-id _) âŸ©âˆ
    trans (sym (cong f (cong g xâ‰¡y))) (trans fgxâ‰¡x xâ‰¡y)         âˆ

-- The circle can be expressed as a suspension.

ğ•ŠÂ¹â†”Susp-Bool : ğ•ŠÂ¹ â†” Susp Bool
ğ•ŠÂ¹â†”Susp-Bool = record
  { surjection = record
    { logical-equivalence = record
      { to   = to
      ; from = from
      }
    ; right-inverse-of = toâˆ˜from
    }
  ; left-inverse-of = fromâˆ˜to
  }
  where
  northâ‰¡north =
    north  â‰¡âŸ¨ meridian false âŸ©
    south  â‰¡âŸ¨ sym $ meridian true âŸ©âˆ
    north  âˆ

  to : ğ•ŠÂ¹ â†’ Susp Bool
  to = Circle.rec north northâ‰¡north

  module From = Rec base base (if_then refl base else loop)

  from : Susp Bool â†’ ğ•ŠÂ¹
  from = From.rec

  toâˆ˜from : âˆ€ x â†’ to (from x) â‰¡ x
  toâˆ˜from = elim _
    (to (from north)  â‰¡âŸ¨âŸ©
     north            âˆ)
    (to (from south)  â‰¡âŸ¨âŸ©
     north            â‰¡âŸ¨ meridian true âŸ©âˆ
     south            âˆ)
    (Î» b â†’
       subst (Î» x â†’ to (from x) â‰¡ x) (meridian b) (refl north)  â‰¡âŸ¨ subst-in-terms-of-trans-and-congâ€² âŸ©

       trans (sym (cong to (cong from (meridian b))))
             (trans (refl north) (meridian b))                  â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym (cong to p)) q) From.rec-meridian (trans-reflË¡ _) âŸ©

       trans (sym (cong to (if b then refl base else loop)))
             (meridian b)                                       â‰¡âŸ¨ lemma b âŸ©âˆ

       meridian true                                            âˆ)
    where
    lemma : (b : Bool) â†’ _ â‰¡ _
    lemma true  =
      trans (sym (cong to (if true â¦‚ Bool then refl base else loop)))
            (meridian true)                                            â‰¡âŸ¨âŸ©

      trans (sym (cong to (refl base))) (meridian true)                â‰¡âŸ¨ prove (Trans (Sym (Cong _ Refl)) (Lift _)) (Lift _) (refl _) âŸ©âˆ

      meridian true                                                    âˆ

    lemma false =
      trans (sym (cong to (if false â¦‚ Bool then refl base else loop)))
            (meridian false)                                            â‰¡âŸ¨âŸ©

      trans (sym (cong to loop)) (meridian false)                       â‰¡âŸ¨ cong (Î» p â†’ trans (sym p) (meridian false)) Circle.rec-loop âŸ©

      trans (sym northâ‰¡north) (meridian false)                          â‰¡âŸ¨ prove (Trans (Sym (Trans (Lift _) (Sym (Lift _)))) (Lift _))
                                                                                 (Trans (Trans (Lift _) (Sym (Lift _))) (Lift _))
                                                                                 (refl _) âŸ©
      trans (trans (meridian true) (sym $ meridian false))
            (meridian false)                                            â‰¡âŸ¨ trans-[trans-sym]- _ _ âŸ©âˆ

      meridian true                                                     âˆ

  fromâˆ˜to : âˆ€ x â†’ from (to x) â‰¡ x
  fromâˆ˜to = Circle.elim _
    (from (to base)  â‰¡âŸ¨âŸ©
     base            âˆ)
    (subst (Î» x â†’ from (to x) â‰¡ x) loop (refl base)                  â‰¡âŸ¨ subst-in-terms-of-trans-and-congâ€² âŸ©

     trans (sym (cong from (cong to loop)))
           (trans (refl base) loop)                                  â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym (cong from p)) q)
                                                                        Circle.rec-loop (trans-reflË¡ _) âŸ©

     trans (sym (cong from northâ‰¡north)) loop                        â‰¡âŸ¨ prove (Trans (Sym (Cong _ (Trans (Lift _) (Sym (Lift _))))) (Lift _))
                                                                              (Trans (Trans (Cong from (Lift (meridian true)))
                                                                                            (Sym (Cong from (Lift (meridian false)))))
                                                                                     (Lift _))
                                                                              (refl _) âŸ©
     trans (trans (cong from (meridian true))
                  (sym $ cong from (meridian false)))
           loop                                                      â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (trans p (sym q)) loop)
                                                                          From.rec-meridian
                                                                          From.rec-meridian âŸ©
     trans (trans (if true â¦‚ Bool then refl base else loop)
                  (sym $ if false â¦‚ Bool then refl base else loop))
           loop                                                      â‰¡âŸ¨âŸ©

     trans (trans (refl base) (sym loop)) loop                       â‰¡âŸ¨ trans-[trans-sym]- _ _ âŸ©âˆ

     refl base                                                       âˆ)

-- The remainder of this module is not based on the HoTT book.

-- The interval can be expressed as a suspension.

Intervalâ†”Susp-âŠ¤ : Interval â†” Susp âŠ¤
Intervalâ†”Susp-âŠ¤ = record
  { surjection = record
    { logical-equivalence = record
      { to   = to
      ; from = from
      }
    ; right-inverse-of = elim
        _
        (refl _)
        (refl _)
        (Î» _ â†’
           subst (Î» x â†’ to (from x) â‰¡ x) (meridian tt) (refl _)           â‰¡âŸ¨ subst-in-terms-of-trans-and-congâ€² âŸ©

           trans (sym (cong to (cong from (meridian tt))))
                 (trans (refl _) (meridian tt))                           â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym (cong to p)) q)
                                                                               (rec-meridian _ _ _)
                                                                               (trans-reflË¡ _) âŸ©
           trans (sym (cong to 0â‰¡1)) (meridian tt)                        â‰¡âŸ¨ cong (Î» p â†’ trans (sym p) (meridian tt)) $ Interval.rec-0â‰¡1 _ _ _ âŸ©

           trans (sym (meridian tt)) (meridian tt)                        â‰¡âŸ¨ trans-symË¡ _ âŸ©âˆ

           refl _                                                         âˆ)
    }
  ; left-inverse-of = Interval.elim
      _
      (refl _)
      (refl _)
      (subst (Î» x â†’ from (to x) â‰¡ x) 0â‰¡1 (refl _)                  â‰¡âŸ¨ subst-in-terms-of-trans-and-congâ€² âŸ©
       trans (sym (cong from (cong to 0â‰¡1))) (trans (refl _) 0â‰¡1)  â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym (cong from p)) q)
                                                                        (Interval.rec-0â‰¡1 _ _ _)
                                                                        (trans-reflË¡ _) âŸ©
       trans (sym (cong from (meridian tt))) 0â‰¡1                   â‰¡âŸ¨ cong (Î» p â†’ trans (sym p) 0â‰¡1) $ rec-meridian _ _ _ âŸ©
       trans (sym 0â‰¡1) 0â‰¡1                                         â‰¡âŸ¨ trans-symË¡ _ âŸ©âˆ
       refl _                                                      âˆ)
  }
  where
  to   = Interval.rec north south (meridian tt)
  from = rec [0] [1] Î» _ â†’ 0â‰¡1

-- A map function.

map : (A â†’ B) â†’ Susp A â†’ Susp B
map Aâ†’B = rec north south (meridian âˆ˜ Aâ†’B)

private

  -- A helper function used to implement cong-â†  and cong-â†”.

  mapâˆ˜map :
    (âˆ€ x â†’ f (g x) â‰¡ x) â†’
    âˆ€ x â†’ map f (map g x) â‰¡ x
  mapâˆ˜map {f = f} {g = g} hyp = elim
    _
    (refl _)
    (refl _)
    (Î» x â†’
       subst (Î» x â†’ map f (map g x) â‰¡ x) (meridian x) (refl _)   â‰¡âŸ¨ subst-in-terms-of-trans-and-congâ€² âŸ©

       trans (sym $ cong (map f) $ cong (map g) (meridian x))
             (trans (refl _) (meridian x))                       â‰¡âŸ¨ congâ‚‚ (Î» p q â†’ trans (sym $ cong (map f) p) q)
                                                                      (rec-meridian _ _ _)
                                                                      (trans-reflË¡ _) âŸ©

       trans (sym $ cong (map f) $ meridian (g x)) (meridian x)  â‰¡âŸ¨ cong (Î» p â†’ trans (sym p) (meridian x)) $ rec-meridian _ _ _ âŸ©

       trans (sym $ meridian (f (g x))) (meridian x)             â‰¡âŸ¨ cong (Î» y â†’ trans (sym $ meridian y) (meridian x)) $ hyp x âŸ©

       trans (sym $ meridian x) (meridian x)                     â‰¡âŸ¨ trans-symË¡ _ âŸ©âˆ

       refl _                                                    âˆ)

-- Some preservation lemmas.

cong-â‡” : A â‡” B â†’ Susp A â‡” Susp B
cong-â‡” Aâ‡”B = record
  { to   = map (_â‡”_.to   Aâ‡”B)
  ; from = map (_â‡”_.from Aâ‡”B)
  }

cong-â†  : A â†  B â†’ Susp A â†  Susp B
cong-â†  Aâ† B = record
  { logical-equivalence = cong-â‡” (_â† _.logical-equivalence Aâ† B)
  ; right-inverse-of    = mapâˆ˜map (_â† _.right-inverse-of Aâ† B)
  }

cong-â†” : A â†” B â†’ Susp A â†” Susp B
cong-â†” Aâ†”B = record
  { surjection      = cong-â†  (_â†”_.surjection Aâ†”B)
  ; left-inverse-of = mapâˆ˜map (_â†”_.left-inverse-of Aâ†”B)
  }

cong-â‰ƒ : A â‰ƒ B â†’ Susp A â‰ƒ Susp B
cong-â‰ƒ = from-isomorphism âˆ˜ cong-â†” âˆ˜ from-isomorphism

private

  -- Lemmas used to implement Â¬-cong-â†£ and Â¬-cong-Embedding.

  âŠ¥â†£âŠ¤ : âŠ¥ {â„“ = â„“â‚} â†£ â†‘ â„“â‚‚ âŠ¤
  âŠ¥â†£âŠ¤ = record
    { to        = Î» ()
    ; injective = Î» {}
    }

  Â¬SuspâŠ¥â†£SuspâŠ¤ : Â¬ (Susp (âŠ¥ {â„“ = â„“â‚}) â†£ Susp (â†‘ â„“â‚‚ âŠ¤))
  Â¬SuspâŠ¥â†£SuspâŠ¤ =
    Susp âŠ¥ â†£ Susp (â†‘ _ âŠ¤)                                          â†âŸ¨ (Î» f â†’ from-isomorphism (cong-â†” Bijection.â†‘â†”) F.âˆ˜ f F.âˆ˜
                                                                             from-isomorphism (cong-â†” âŠ¥â†”âŠ¥)) âŸ©
    Susp âŠ¥â‚€ â†£ Susp âŠ¤                                               â†âŸ¨ (Î» f â†’ from-isomorphism (inverse Intervalâ†”Susp-âŠ¤) F.âˆ˜ f F.âˆ˜
                                                                             from-isomorphism Boolâ†”Susp-âŠ¥) âŸ©
    Bool â†£ Interval                                                â†âŸ¨ (Î» inj â†’ _â†£_.to inj , _â†£_.injective inj) âŸ©
    (âˆƒ Î» (f : Bool â†’ Interval) â†’ f true â‰¡ f false â†’ true â‰¡ false)  â†âŸ¨ Î£-map id (Î» f â†’ f (monoâ‚ 0 Interval.interval-contractible _ _)) âŸ©
    (Bool â†’ Interval) Ã— true â‰¡ false                               â†âŸ¨ projâ‚‚ âŸ©
    true â‰¡ false                                                   â†âŸ¨ Bool.trueâ‰¢false âŸ©â–¡
    âŠ¥                                                              â–¡

-- Some negative preservation results.

Â¬-cong-â†£ :
  Â¬ (âˆ€ {A : Type a} {B : Type b} â†’ A â†£ B â†’ Susp A â†£ Susp B)
Â¬-cong-â†£ {a = a} {b = b} =
  (âˆ€ {A B} â†’ A â†£ B â†’ Susp A â†£ Susp B)  â†âŸ¨ (Î» hyp â†’ hyp) âŸ©
  (âŠ¥ â†£ â†‘ _ âŠ¤ â†’ Susp âŠ¥ â†£ Susp (â†‘ _ âŠ¤))  â†âŸ¨ _$ âŠ¥â†£âŠ¤ âŸ©
  Susp âŠ¥ â†£ Susp (â†‘ _ âŠ¤)                â†âŸ¨ Â¬SuspâŠ¥â†£SuspâŠ¤ âŸ©â–¡
  âŠ¥                                    â–¡

Â¬-cong-Embedding :
  Â¬ (âˆ€ {A : Type a} {B : Type b} â†’
     Embedding A B â†’ Embedding (Susp A) (Susp B))
Â¬-cong-Embedding =
  (âˆ€ {A B} â†’ Embedding A B â†’ Embedding (Susp A) (Susp B))    â†âŸ¨ (Î» hyp â†’ hyp) âŸ©
  (Embedding âŠ¥ (â†‘ _ âŠ¤) â†’ Embedding (Susp âŠ¥) (Susp (â†‘ _ âŠ¤)))  â†âŸ¨ _$ Emb-âŠ¥-âŠ¤ âŸ©
  Embedding (Susp âŠ¥) (Susp (â†‘ _ âŠ¤))                          â†âŸ¨ Embedding.injection âŸ©
  Susp âŠ¥ â†£ Susp (â†‘ _ âŠ¤)                                      â†âŸ¨ Â¬SuspâŠ¥â†£SuspâŠ¤ âŸ©â–¡
  âŠ¥                                                          â–¡
  where
  Emb-âŠ¥-âŠ¤ : Embedding âŠ¥ (â†‘ _ âŠ¤)
  Emb-âŠ¥-âŠ¤ =
    _â†”_.to (Embedding.â†£â†”Embedding
              ext
              (monoâ‚ 1 âŠ¥-propositional)
              (mono (Nat.zeroâ‰¤ 2) (â†‘-closure 0 âŠ¤-contractible)))
           âŠ¥â†£âŠ¤
