------------------------------------------------------------------------
-- Localisation
------------------------------------------------------------------------

{-# OPTIONS --erased-cubical --safe #-}

-- Following "Modalities in Homotopy Type Theory" by Rijke, Shulman
-- and Spitters.

-- The module is parametrised by a notion of equality. The higher
-- constructors of the HIT defining localisation use path equality,
-- but the supplied notion of equality is used for many other things.

import Equality.Path as P

module Localisation
  {eâº} (eq : âˆ€ {a p} â†’ P.Equality-with-paths a p eâº) where

open P.Derived-definitions-and-properties eq hiding (elim)

open import Prelude as P

open import Bijection equality-with-J using (_â†”_)
open import Equality.Path.Isomorphisms eq hiding (ext)
open import Pushout eq as Pushout using (Pushout)

private
  variable
    a b c p q r : Level
    A B C       : Type a
    P Q R       : A â†’ Type p
    e f g x y   : A

------------------------------------------------------------------------
-- Localisation

-- A first approximation to localisation.
--
-- This is a slight generalisation of the HIT that Rijke et al. call
-- ğ“™: they require all types to live in the same universe.

data Localisationâ€²
       {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
       (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b) :
       Type (a âŠ” b âŠ” p âŠ” q) where
  [_]   : B â†’ Localisationâ€² f B
  ext   : (P x â†’ Localisationâ€² f B) â†’
          (Q x â†’ Localisationâ€² f B)
  extâ‰¡á´¾ : ext g (f x y) P.â‰¡ g y

-- A variant of extâ‰¡á´¾.

extâ‰¡ :
  {f : (x : A) â†’ P x â†’ Q x} {g : P x â†’ Localisationâ€² f B} â†’
  ext g (f x y) â‰¡ g y
extâ‰¡ = _â†”_.from â‰¡â†”â‰¡ extâ‰¡á´¾

-- Localisation.

Localisation :
  {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q} â†’
  (âˆ€ x â†’ P x â†’ Q x) â†’
  Type b â†’ Type (a âŠ” b âŠ” p âŠ” q)
Localisation {p = p} {q = q} {A = A} {P = P} {Q = Q} f =
  Localisationâ€² fÌ‚
  where
  PÌ‚ : A âŠ A â†’ Type (p âŠ” q)
  PÌ‚ = P.[ â†‘ q âˆ˜ P
        , (Î» x â†’ Pushout (record { left = f x; right = f x }))
        ]

  QÌ‚ : A âŠ A â†’ Type q
  QÌ‚ = P.[ Q , Q ]

  fÌ‚ : (x : A âŠ A) â†’ PÌ‚ x â†’ QÌ‚ x
  fÌ‚ = P.[ (Î» x â†’ f x âˆ˜ lower)
        , (Î» x â†’ Pushout.rec id id (refl âˆ˜ f x))
        ]

------------------------------------------------------------------------
-- Some eliminators for Localisationâ€²

-- A dependent eliminator, expressed using paths.

record Elimá´¾
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b)
         (R : Localisationâ€² f B â†’ Type r) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” r) where
  no-eta-equality
  field
    []Ê³   : âˆ€ x â†’ R [ x ]
    extÊ³  : (âˆ€ y â†’ R (g y)) â†’ âˆ€ y â†’ R (ext {x = x} g y)
    extâ‰¡Ê³ : (h : (y : P x) â†’ R (g y)) â†’
            P.[ (Î» i â†’ R (extâ‰¡á´¾ {g = g} {y = y} i)) ] extÊ³ h (f x y) â‰¡
            h y

open Elimá´¾ public

elimá´¾ : Elimá´¾ f B R â†’ (x : Localisationâ€² f B) â†’ R x
elimá´¾ {f = f} {B = B} {R = R} e = helper
  where
  module E = Elimá´¾ e

  helper : (x : Localisationâ€² f B) â†’ R x
  helper [ x ]             = E.[]Ê³ x
  helper (ext g y)         = E.extÊ³ (Î» y â†’ helper (g y)) y
  helper (extâ‰¡á´¾ {g = g} i) = E.extâ‰¡Ê³ (Î» y â†’ helper (g y)) i

-- A non-dependent eliminator, expressed using paths.

record Recá´¾
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b)
         (C : Type c) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” c) where
  no-eta-equality
  field
    []Ê³   : B â†’ C
    extÊ³  : (P x â†’ C) â†’ Q x â†’ C
    extâ‰¡Ê³ : (g : P x â†’ C) â†’ extÊ³ g (f x y) P.â‰¡ g y

open Recá´¾ public

recá´¾ : Recá´¾ f B C â†’ Localisationâ€² f B â†’ C
recá´¾ r = elimá´¾ Î» where
    .[]Ê³   â†’ R.[]Ê³
    .extÊ³  â†’ R.extÊ³
    .extâ‰¡Ê³ â†’ R.extâ‰¡Ê³
  where
  module R = Recá´¾ r

-- A dependent eliminator.

record Elim
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b)
         (R : Localisationâ€² f B â†’ Type r) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” r) where
  no-eta-equality
  field
    []Ê³   : âˆ€ x â†’ R [ x ]
    extÊ³  : (âˆ€ y â†’ R (g y)) â†’ âˆ€ y â†’ R (ext {x = x} g y)
    extâ‰¡Ê³ : (h : (y : P x) â†’ R (g y)) â†’
            subst R (extâ‰¡ {y = y} {g = g}) (extÊ³ h (f x y)) â‰¡ h y

open Elim public

elim : Elim f B R â†’ (x : Localisationâ€² f B) â†’ R x
elim e = elimá´¾ Î» where
    .[]Ê³   â†’ E.[]Ê³
    .extÊ³  â†’ E.extÊ³
    .extâ‰¡Ê³ â†’ substâ‰¡â†’[]â‰¡ âˆ˜ E.extâ‰¡Ê³
  where
  module E = Elim e

-- A "computation" rule.

elim-extâ‰¡ :
  dcong (elim e) (extâ‰¡ {y = y} {g = g}) â‰¡
  Elim.extâ‰¡Ê³ e (elim e âˆ˜ g)
elim-extâ‰¡ = dcong-substâ‰¡â†’[]â‰¡ (refl _)

-- A non-dependent eliminator.

record Rec
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b)
         (C : Type c) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” c) where
  no-eta-equality
  field
    []Ê³   : B â†’ C
    extÊ³  : (P x â†’ C) â†’ Q x â†’ C
    extâ‰¡Ê³ : (g : P x â†’ C) â†’ extÊ³ g (f x y) â‰¡ g y

open Rec public

rec : Rec f B C â†’ Localisationâ€² f B â†’ C
rec r = recá´¾ Î» where
    .[]Ê³   â†’ R.[]Ê³
    .extÊ³  â†’ R.extÊ³
    .extâ‰¡Ê³ â†’ _â†”_.to â‰¡â†”â‰¡ âˆ˜ R.extâ‰¡Ê³
  where
  module R = Rec r

-- A "computation" rule.

rec-extâ‰¡ :
  {f : âˆ€ x â†’ P x â†’ Q x}
  {r : Rec f B C}
  {g : P x â†’ Localisationâ€² f B} â†’
  cong (rec r) (extâ‰¡ {y = y} {g = g}) â‰¡
  Rec.extâ‰¡Ê³ r (rec r âˆ˜ g)
rec-extâ‰¡ = cong-â‰¡â†”â‰¡ (refl _)
