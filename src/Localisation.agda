------------------------------------------------------------------------
-- Localisation
------------------------------------------------------------------------

{-# OPTIONS --erased-cubical --safe #-}

-- Following "Modalities in Homotopy Type Theory" by Rijke, Shulman
-- and Spitters.

-- The module is parametrised by a notion of equality. The higher
-- constructors of the HIT defining (a first approximation to)
-- localisation use path equality, but the supplied notion of equality
-- is used for many other things.

import Equality.Path as P

module Localisation
  {eâº} (eq : âˆ€ {a p} â†’ P.Equality-with-paths a p eâº) where

open P.Derived-definitions-and-properties eq hiding (elim)

open import Logical-equivalence using (_â‡”_)
open import Prelude as P

open import Bijection equality-with-J as B using (_â†”_)
open import Equality.Path.Isomorphisms eq as I hiding (ext)
open import Equivalence equality-with-J as Eq
  using (_â‰ƒ_; Is-equivalence)
open import Equivalence.Path-split equality-with-J as PS
  using (Path-split)
open import Extensionality equality-with-J
open import Function-universe equality-with-J as F hiding (_âˆ˜_)
open import H-level equality-with-J
open import H-level.Closure equality-with-J
open import Pullback equality-with-J as PB using (âˆ†)
open import Pushout eq as PO using (Pushout; âˆ‡; Pushoutâ†’â‰ƒPullback)
open import Surjection equality-with-J using (_â† _; Split-surjective)

private
  variable
    a b c p q r : Level
    A B C       : Type a
    P Q R       : A â†’ Type p
    e f g x y   : A

------------------------------------------------------------------------
-- Local types

-- A type B is f-local (for a family of functions fÂ :Â âˆ€Â xÂ â†’Â PÂ xÂ â†’Â QÂ x)
-- if precomposition with fÂ x (where the codomain of the composition
-- operation is B) is an equivalence for all x.

_-Local_ :
  {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q} â†’
  (f : âˆ€ x â†’ P x â†’ Q x) â†’ Type b â†’ Type (a âŠ” b âŠ” p âŠ” q)
_-Local_ {Q = Q} f B =
  âˆ€ x â†’ Is-equivalence (Î» (g : Q x â†’ B) â†’ g âˆ˜ f x)

-- Locality can be expressed in another way.

Localâ‰ƒSplit-surjective-âˆ˜Ã—Split-surjective-âˆ˜âˆ‡ :
  {f : (x : A) â†’ P x â†’ Q x} â†’
  f -Local B â‰ƒ
  (âˆ€ x â†’ Split-surjective ((_âˆ˜ f x)     â¦‚ ((_ â†’ B) â†’ _)) Ã—
         Split-surjective ((_âˆ˜ âˆ‡ (f x)) â¦‚ ((_ â†’ B) â†’ _)))
Localâ‰ƒSplit-surjective-âˆ˜Ã—Split-surjective-âˆ˜âˆ‡
  {P = P} {Q = Q} {B = B} {f = f} =
  f -Local B                                                         â†”âŸ¨âŸ©
  (âˆ€ x â†’ Is-equivalence (_âˆ˜ f x))                                    â†âŸ¨ (âˆ€-cong I.ext Î» x â†’ lemma (f x)) âŸ©â–¡
  (âˆ€ x â†’ Split-surjective (_âˆ˜ f x) Ã— Split-surjective (_âˆ˜ âˆ‡ (f x)))  â–¡
  where
  lemma : (g : P x â†’ Q x) â†’ _
  lemma g =
    Is-equivalence (_âˆ˜ g)                                   â†âŸ¨ inverse $ PS.Path-splitâ†”Is-equivalence I.ext âŸ©

    Path-split 2 (_âˆ˜ g)                                     â†âŸ¨ PS.Path-split-2â‰ƒSplit-surjectiveÃ—Split-surjective-âˆ† I.ext âŸ©

    Split-surjective (_âˆ˜ g) Ã— Split-surjective (âˆ† (_âˆ˜ g))   â†âŸ¨ (âˆƒ-cong Î» _ â†’ inverse $ Split-surjective-cong I.ext $ extâ»Â¹
                                                                PO.âˆ˜âˆ‡â‰¡âˆ†âˆ˜) âŸ©
    Split-surjective (_âˆ˜ g) Ã—
    Split-surjective (_â‰ƒ_.to Pushoutâ†’â‰ƒPullback âˆ˜ (_âˆ˜ âˆ‡ g))  â†âŸ¨ (âˆƒ-cong Î» _ â†’ inverse $
                                                                Split-surjectiveâ‰ƒSplit-surjective-âˆ˜Ë¡ I.ext
                                                                  (_â‰ƒ_.is-equivalence Pushoutâ†’â‰ƒPullback)) âŸ©â–¡
    Split-surjective (_âˆ˜ g) Ã— Split-surjective (_âˆ˜ âˆ‡ g)     â–¡

-- Locality can be expressed in a third way.

Localâ‰ƒIs-equivalence-âˆ˜Ã—Is-equivalence-âˆ˜âˆ‡ :
  {f : (x : A) â†’ P x â†’ Q x} â†’
  f -Local B â‰ƒ
  (âˆ€ x â†’ Is-equivalence ((_âˆ˜ f x)     â¦‚ ((_ â†’ B) â†’ _)) Ã—
         Is-equivalence ((_âˆ˜ âˆ‡ (f x)) â¦‚ ((_ â†’ B) â†’ _)))
Localâ‰ƒIs-equivalence-âˆ˜Ã—Is-equivalence-âˆ˜âˆ‡ {P = P} {Q = Q} {B = B} {f = f} =
  f -Local B                                                     â†”âŸ¨âŸ©
  (âˆ€ x â†’ Is-equivalence (_âˆ˜ f x))                                â†âŸ¨ (âˆ€-cong I.ext Î» x â†’ lemma (f x)) âŸ©â–¡
  (âˆ€ x â†’ Is-equivalence (_âˆ˜ f x) Ã— Is-equivalence (_âˆ˜ âˆ‡ (f x)))  â–¡
  where
  lemma : (g : P x â†’ Q x) â†’ _
  lemma g =
    Is-equivalence (_âˆ˜ g)                              â†”âŸ¨ (inverse $ drop-âŠ¤-right Î» âˆ˜-f-eq â†’
                                                           _â‡”_.to contractibleâ‡”â†”âŠ¤ $
                                                           propositionalâ‡’inhabitedâ‡’contractible
                                                             (Is-equivalence-propositional I.ext)
                                                             (PB.Is-equivalence-âˆ† âˆ˜-f-eq)) âŸ©
    Is-equivalence (_âˆ˜ g) Ã— Is-equivalence (âˆ† (_âˆ˜ g))  â†âŸ¨ (âˆƒ-cong Î» _ â†’ PO.Is-equivalence-âˆ†âˆ˜â‰ƒIs-equivalence-âˆ˜âˆ‡) âŸ©â–¡
    Is-equivalence (_âˆ˜ g) Ã— Is-equivalence (_âˆ˜ âˆ‡ g)    â–¡

------------------------------------------------------------------------
-- Localisationâ€²

-- A first approximation to localisation.
--
-- This is a slight generalisation of the HIT that Rijke et al. call
-- ğ“™: they require all types to live in the same universe.

data Localisationâ€²
       {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
       (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b) :
       Type (a âŠ” b âŠ” p âŠ” q) where
  [_]   : B â†’ Localisationâ€² f B
  ext   : âˆ€ x â†’ (P x â†’ Localisationâ€² f B) â†’ (Q x â†’ Localisationâ€² f B)
  extâ‰¡á´¾ : ext x g (f x y) P.â‰¡ g y

-- A variant of extâ‰¡á´¾.

extâ‰¡ :
  {f : (x : A) â†’ P x â†’ Q x} {g : P x â†’ Localisationâ€² f B} â†’
  ext x g (f x y) â‰¡ g y
extâ‰¡ = _â†”_.from â‰¡â†”â‰¡ extâ‰¡á´¾

------------------------------------------------------------------------
-- Some eliminators for Localisationâ€²

-- A dependent eliminator, expressed using paths.

record Elimá´¾
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         {f : âˆ€ x â†’ P x â†’ Q x} {B : Type b}
         (R : Localisationâ€² f B â†’ Type r) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” r) where
  no-eta-equality
  field
    []Ê³   : âˆ€ x â†’ R [ x ]
    extÊ³  : ((y : P x) â†’ R (g y)) â†’ âˆ€ y â†’ R (ext x g y)
    extâ‰¡Ê³ : (h : (y : P x) â†’ R (g y)) â†’
            P.[ (Î» i â†’ R (extâ‰¡á´¾ {g = g} {y = y} i)) ] extÊ³ h (f x y) â‰¡
            h y

open Elimá´¾ public

elimá´¾ : Elimá´¾ R â†’ (x : Localisationâ€² f B) â†’ R x
elimá´¾ {f = f} {B = B} {R = R} e = helper
  where
  module E = Elimá´¾ e

  helper : (x : Localisationâ€² f B) â†’ R x
  helper [ x ]             = E.[]Ê³ x
  helper (ext _ g y)       = E.extÊ³ (Î» y â†’ helper (g y)) y
  helper (extâ‰¡á´¾ {g = g} i) = E.extâ‰¡Ê³ (Î» y â†’ helper (g y)) i

-- A non-dependent eliminator, expressed using paths.

record Recá´¾
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b)
         (C : Type c) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” c) where
  no-eta-equality
  field
    []Ê³   : B â†’ C
    extÊ³  : (P x â†’ C) â†’ Q x â†’ C
    extâ‰¡Ê³ : (g : P x â†’ C) â†’ extÊ³ g (f x y) P.â‰¡ g y

open Recá´¾ public

recá´¾ : Recá´¾ f B C â†’ Localisationâ€² f B â†’ C
recá´¾ r = elimá´¾ Î» where
    .[]Ê³   â†’ R.[]Ê³
    .extÊ³  â†’ R.extÊ³
    .extâ‰¡Ê³ â†’ R.extâ‰¡Ê³
  where
  module R = Recá´¾ r

-- A dependent eliminator.

record Elim
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         {f : âˆ€ x â†’ P x â†’ Q x} {B : Type b}
         (R : Localisationâ€² f B â†’ Type r) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” r) where
  no-eta-equality
  field
    []Ê³   : âˆ€ x â†’ R [ x ]
    extÊ³  : ((y : P x) â†’ R (g y)) â†’ âˆ€ y â†’ R (ext x g y)
    extâ‰¡Ê³ : (h : (y : P x) â†’ R (g y)) â†’
            subst R (extâ‰¡ {y = y} {g = g}) (extÊ³ h (f x y)) â‰¡ h y

open Elim public

elim : Elim R â†’ (x : Localisationâ€² f B) â†’ R x
elim {R = R} e = elimá´¾ eá´¾
  where
  module E = Elim e

  eá´¾ : Elimá´¾ R
  eá´¾ .[]Ê³   = E.[]Ê³
  eá´¾ .extÊ³  = E.extÊ³
  eá´¾ .extâ‰¡Ê³ = substâ‰¡â†’[]â‰¡ âˆ˜ E.extâ‰¡Ê³

-- A "computation rule".

elim-extâ‰¡ :
  dcong (elim e) (extâ‰¡ {y = y} {g = g}) â‰¡
  e .extâ‰¡Ê³ (elim e âˆ˜ g)
elim-extâ‰¡ = dcong-substâ‰¡â†’[]â‰¡ (refl _)

-- A non-dependent eliminator.

record Rec
         {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q}
         (f : âˆ€ x â†’ P x â†’ Q x) (B : Type b)
         (C : Type c) :
         Type (a âŠ” b âŠ” p âŠ” q âŠ” c) where
  no-eta-equality
  field
    []Ê³   : B â†’ C
    extÊ³  : (P x â†’ C) â†’ Q x â†’ C
    extâ‰¡Ê³ : (g : P x â†’ C) â†’ extÊ³ g (f x y) â‰¡ g y

open Rec public

rec : Rec f B C â†’ Localisationâ€² f B â†’ C
rec {f = f} {B = B} {C = C} r = recá´¾ rá´¾
  where
  module R = Rec r

  rá´¾ : Recá´¾ f B C
  rá´¾ .[]Ê³   = R.[]Ê³
  rá´¾ .extÊ³  = R.extÊ³
  rá´¾ .extâ‰¡Ê³ = _â†”_.to â‰¡â†”â‰¡ âˆ˜ R.extâ‰¡Ê³

-- A "computation rule".

rec-extâ‰¡ :
  {f : âˆ€ x â†’ P x â†’ Q x}
  {r : Rec f B C}
  {g : P x â†’ Localisationâ€² f B} â†’
  cong (rec r) (extâ‰¡ {y = y} {g = g}) â‰¡
  r .extâ‰¡Ê³ (rec r âˆ˜ g)
rec-extâ‰¡ = cong-â‰¡â†”â‰¡ (refl _)

------------------------------------------------------------------------
-- Some lemmas related to Localisationâ€²

-- If C is f-local, then precomposition with [_] (at a certain type)
-- is an equivalence.

Localâ†’Is-equivalence-âˆ˜[] :
  {f : âˆ€ x â†’ P x â†’ Q x} â†’
  f -Local C â†’
  Is-equivalence (Î» (g : Localisationâ€² f B â†’ C) â†’ g âˆ˜ [_])
Localâ†’Is-equivalence-âˆ˜[] {P = P} {Q = Q} {C = C} {B = B} {f = f} local =
                           $âŸ¨ (Î» g â†’ from g , from-[])
                            , (Î» g h â†’
                                   (Î» gâˆ˜[]â‰¡hâˆ˜[] â†’
                                        drop-âˆ˜[] g h gâˆ˜[]â‰¡hâˆ˜[]
                                      , cong-âˆ˜[]-drop-âˆ˜[] gâˆ˜[]â‰¡hâˆ˜[])
                                 , _)
                            âŸ©
  Path-split 2 (_âˆ˜ [_])    â†’âŸ¨ PS.Path-splitâ†”Is-equivalence _ âŸ©â–¡
  Is-equivalence (_âˆ˜ [_])  â–¡
  where
  Qâ†’Câ‰ƒPâ†’C : âˆ€ x â†’ (Q x â†’ C) â‰ƒ (P x â†’ C)
  Qâ†’Câ‰ƒPâ†’C x = Eq.âŸ¨ _âˆ˜ f x , local x âŸ©

  from : (B â†’ C) â†’ (Localisationâ€² f B â†’ C)
  from g = elim Î» where
    .[]Ê³          â†’ g
    .extÊ³ {x = x} â†’
      (P x â†’ C)  â†”âŸ¨ inverse $ Qâ†’Câ‰ƒPâ†’C x âŸ©â–¡
      (Q x â†’ C)  â–¡
    .extâ‰¡Ê³ {x = x} {y = y} h â†’
      subst (Î» _ â†’ C) extâ‰¡ (_â‰ƒ_.from (Qâ†’Câ‰ƒPâ†’C x) h (f x y))  â‰¡âŸ¨ subst-const _ âŸ©
      _â‰ƒ_.from (Qâ†’Câ‰ƒPâ†’C x) h (f x y)                         â‰¡âŸ¨âŸ©
      _â‰ƒ_.to (Qâ†’Câ‰ƒPâ†’C x) (_â‰ƒ_.from (Qâ†’Câ‰ƒPâ†’C x) h) y          â‰¡âŸ¨ cong (_$ y) $ _â‰ƒ_.right-inverse-of (Qâ†’Câ‰ƒPâ†’C x) _ âŸ©âˆ
      h y                                                    âˆ

  from-[] : from g âˆ˜ [_] â‰¡ g
  from-[] = refl _

  drop-âˆ˜[]â€² :
    (g h : Localisationâ€² f B â†’ C) â†’
    g âˆ˜ [_] â‰¡ h âˆ˜ [_] â†’ âˆ€ x â†’ g x â‰¡ h x
  drop-âˆ˜[]â€² g h gâˆ˜[]â‰¡hâˆ˜[] = elim Î» where
      .[]Ê³ x â†’ cong (_$ x) gâˆ˜[]â‰¡hâˆ˜[]

      .extÊ³ {g = k} â†’ _â‰ƒ_.to (lemma k)

      .extâ‰¡Ê³ {x = x} {g = k} {y = y} gâˆ˜kâ‰¡hâˆ˜k â†’
        subst (Î» x â†’ g x â‰¡ h x) extâ‰¡ (_â‰ƒ_.to (lemma k) gâˆ˜kâ‰¡hâˆ˜k (f x y))  â‰¡âŸ¨ sym $ from-lemma _ _ âŸ©
        _â‰ƒ_.from (lemma k) (_â‰ƒ_.to (lemma k) gâˆ˜kâ‰¡hâˆ˜k) y                  â‰¡âŸ¨ cong (_$ y) $ _â‰ƒ_.left-inverse-of (lemma k) _ âŸ©âˆ
        gâˆ˜kâ‰¡hâˆ˜k y                                                        âˆ
    where
    lemma : âˆ€ {x} (k : P x â†’ Localisationâ€² f B) â†’ _ â‰ƒ _
    lemma {x = x} k =
      ((y : P x) â†’ g (k y) â‰¡ h (k y))              â†”âŸ¨ Î â‰¡â‰ƒâ‰¡ âŸ©
      g âˆ˜ k â‰¡ h âˆ˜ k                                â†”âŸ¨ (â‰¡â‡’â† equivalence $ cong (Î» f â†’ g âˆ˜ f â‰¡ h âˆ˜ f) $ âŸ¨extâŸ© Î» _ â†’ sym extâ‰¡) âŸ©
      g âˆ˜ ext x k âˆ˜ f x â‰¡ h âˆ˜ ext x k âˆ˜ f x        â†”âŸ¨ Eq.â‰ƒ-â‰¡ $ Qâ†’Câ‰ƒPâ†’C x âŸ©
      g âˆ˜ ext x k â‰¡ h âˆ˜ ext x k                    â†”âŸ¨ inverse Î â‰¡â‰ƒâ‰¡ âŸ©â–¡
      ((y : Q x) â†’ g (ext x k y) â‰¡ h (ext x k y))  â–¡

    from-lemma :
      âˆ€ {x y}
      (k : P x â†’ Localisationâ€² f B)
      (eq : âˆ€ y â†’ g (ext x k y) â‰¡ h (ext x k y)) â†’
      _
    from-lemma {x = x} {y = y} k eq =
      _â‰ƒ_.from (lemma k) eq y                          â‰¡âŸ¨âŸ©

      cong (_$ y)
        (_â‰ƒ_.from (â‰¡â‡’â† _ $ cong (Î» f â†’ g âˆ˜ f â‰¡ h âˆ˜ f)
                             (âŸ¨extâŸ© Î» _ â†’ sym extâ‰¡))
           (cong (_âˆ˜ f x) (âŸ¨extâŸ© eq)))                 â‰¡âŸ¨ cong (cong _) $ sym $
                                                          subst-in-terms-of-inverseâˆ˜â‰¡â‡’â† equivalence _ _ _ âŸ©
      cong (_$ y)
        (subst (Î» f â†’ g âˆ˜ f â‰¡ h âˆ˜ f)
           (sym $ âŸ¨extâŸ© Î» _ â†’ sym extâ‰¡)
           (cong (_âˆ˜ f x) (âŸ¨extâŸ© eq)))                 â‰¡âŸ¨ (cong (cong _) $ cong (flip (subst _) _) $
                                                           trans (sym $ ext-sym I.ext) $
                                                           cong âŸ¨extâŸ© $ âŸ¨extâŸ© Î» _ â†’
                                                           sym-sym _) âŸ©
      cong (_$ y)
        (subst (Î» f â†’ g âˆ˜ f â‰¡ h âˆ˜ f)
           (âŸ¨extâŸ© Î» _ â†’ extâ‰¡)
           (cong (_âˆ˜ f x) (âŸ¨extâŸ© eq)))                 â‰¡âŸ¨ cong (cong _) $ cong (subst _ _) $
                                                          cong-pre-âˆ˜-ext I.ext I.ext âŸ©
      cong (_$ y)
        (subst (Î» f â†’ g âˆ˜ f â‰¡ h âˆ˜ f)
           (âŸ¨extâŸ© Î» _ â†’ extâ‰¡)
           (âŸ¨extâŸ© (eq âˆ˜ f x)))                         â‰¡âŸ¨ cong (cong _)
                                                          subst-in-terms-of-trans-and-cong âŸ©
      cong (_$ y)
        (trans (sym (cong (g âˆ˜_) (âŸ¨extâŸ© Î» _ â†’ extâ‰¡)))
           (trans (âŸ¨extâŸ© (eq âˆ˜ f x))
              (cong (h âˆ˜_) (âŸ¨extâŸ© Î» _ â†’ extâ‰¡))))       â‰¡âŸ¨ cong (cong _) $
                                                          trans (congâ‚‚ trans
                                                                   (trans (cong sym $ cong-post-âˆ˜-ext I.ext I.ext) $
                                                                    sym $ ext-sym I.ext)
                                                                   (trans (cong (trans _) $ cong-post-âˆ˜-ext I.ext I.ext) $
                                                                    sym $ ext-trans I.ext)) $
                                                          sym $ ext-trans I.ext âŸ©
      (cong (_$ y) $ âŸ¨extâŸ© Î» y â†’
       trans (sym (cong g extâ‰¡))
         (trans (eq (f x y)) (cong h extâ‰¡)))           â‰¡âŸ¨ cong-ext I.ext âŸ©

      trans (sym (cong g extâ‰¡))
        (trans (eq (f x y)) (cong h extâ‰¡))             â‰¡âŸ¨ sym subst-in-terms-of-trans-and-cong âŸ©âˆ

      subst (Î» x â†’ g x â‰¡ h x) extâ‰¡ (eq (f x y))        âˆ

  drop-âˆ˜[] :
    (g h : Localisationâ€² f B â†’ C) â†’
    g âˆ˜ [_] â‰¡ h âˆ˜ [_] â†’ g â‰¡ h
  drop-âˆ˜[] g h gâˆ˜[]â‰¡hâˆ˜[] = âŸ¨extâŸ© $ drop-âˆ˜[]â€² g h gâˆ˜[]â‰¡hâˆ˜[]

  cong-âˆ˜[]-drop-âˆ˜[] :
    {g h : Localisationâ€² f B â†’ C}
    (gâˆ˜[]â‰¡hâˆ˜[] : g âˆ˜ [_] â‰¡ h âˆ˜ [_]) â†’
    cong (_âˆ˜ [_]) (drop-âˆ˜[] g h gâˆ˜[]â‰¡hâˆ˜[]) â‰¡ gâˆ˜[]â‰¡hâˆ˜[]
  cong-âˆ˜[]-drop-âˆ˜[] {g = g} {h = h} gâˆ˜[]â‰¡hâˆ˜[] =
    cong (_âˆ˜ [_]) (drop-âˆ˜[] g h gâˆ˜[]â‰¡hâˆ˜[])           â‰¡âŸ¨âŸ©
    cong (_âˆ˜ [_]) (âŸ¨extâŸ© $ drop-âˆ˜[]â€² g h gâˆ˜[]â‰¡hâˆ˜[])  â‰¡âŸ¨ cong-pre-âˆ˜-ext I.ext I.ext âŸ©
    âŸ¨extâŸ© (drop-âˆ˜[]â€² g h gâˆ˜[]â‰¡hâˆ˜[] âˆ˜ [_])            â‰¡âŸ¨âŸ©
    âŸ¨extâŸ© (extâ»Â¹ gâˆ˜[]â‰¡hâˆ˜[])                          â‰¡âŸ¨ _â‰ƒ_.right-inverse-of Î â‰¡â‰ƒâ‰¡ _ âŸ©âˆ
    gâˆ˜[]â‰¡hâˆ˜[]                                        âˆ

-- If fÂ x is split surjective for each x, then Localisationâ€²Â fÂ B is
-- f-local.

Split-surjectiveâ†’Local-Localisationâ€² :
  {f : (x : A) â†’ P x â†’ Q x} â†’
  (âˆ€ x â†’ Split-surjective (f x)) â†’
  f -Local Localisationâ€² f B
Split-surjectiveâ†’Local-Localisationâ€² {P = P} {Q = Q} {f = f} f-surj x =
  _â‰ƒ_.is-equivalence $
  Eq.â†”â†’â‰ƒ
    _
    (ext x)
    (Î» _ â†’ âŸ¨extâŸ© Î» _ â†’ extâ‰¡)
    (Î» g â†’ âŸ¨extâŸ© Î» y â†’
       ext x (g âˆ˜ f x) y                         â‰¡âŸ¨ cong (ext _ _) $ sym $ _â† _.right-inverse-of Pxâ† Qx _ âŸ©
       ext x (g âˆ˜ f x) (f x (_â† _.from Pxâ† Qx y))  â‰¡âŸ¨ extâ‰¡ âŸ©
       g (f x (_â† _.from Pxâ† Qx y))                â‰¡âŸ¨ cong g $ _â† _.right-inverse-of Pxâ† Qx _ âŸ©âˆ
       g y                                       âˆ)
  where
  Pxâ† Qx : P x â†  Q x
  Pxâ† Qx = _â†”_.from â† â†”âˆƒ-Split-surjective (f x , f-surj x)

------------------------------------------------------------------------
-- Localisation

-- The localisation operation.

Localisation :
  {A : Type a} {P : A â†’ Type p} {Q : A â†’ Type q} â†’
  (âˆ€ x â†’ P x â†’ Q x) â†’
  Type b â†’ Type (a âŠ” b âŠ” p âŠ” q)
Localisation {p = p} {q = q} {A = A} {P = P} {Q = Q} f =
  Localisationâ€² fÌ‚
  where
  PÌ‚ : A âŠ A â†’ Type (p âŠ” q)
  PÌ‚ = P.[ â†‘ q âˆ˜ P
        , (Î» x â†’ Pushout (record { left = f x; right = f x }))
        ]

  QÌ‚ : A âŠ A â†’ Type q
  QÌ‚ = P.[ Q , Q ]

  fÌ‚ : (x : A âŠ A) â†’ PÌ‚ x â†’ QÌ‚ x
  fÌ‚ = P.[ (Î» x â†’ f x âˆ˜ lower)
        , (Î» x â†’ âˆ‡ (f x))
        ]

-- LocalisationÂ fÂ B is f-local.

Local-Localisation : f -Local Localisation f B
Local-Localisation {f = f} {B = B} =
  _â‰ƒ_.from Localâ‰ƒSplit-surjective-âˆ˜Ã—Split-surjective-âˆ˜âˆ‡ Î» x â†’
    (Î» g â†’ ext (injâ‚ x) (g âˆ˜ lower)
         , âŸ¨extâŸ© Î» y â†’
             ext (injâ‚ x) (g âˆ˜ lower) (f x y)  â‰¡âŸ¨ extâ‰¡ âŸ©âˆ
             g y                               âˆ)
  , (Î» g â†’ ext (injâ‚‚ x) g
         , (âŸ¨extâŸ© $ PO.elim
              (Î» y â†’ ext (injâ‚‚ x) g (âˆ‡ (f x) y) â‰¡ g y)
              (Î» _ â†’ extâ‰¡)
              (Î» _ â†’ extâ‰¡)
              (lemma x g)))
  where
  lemma :
    âˆ€ x g y â†’
    subst (Î» y â†’ ext (injâ‚‚ x) g (âˆ‡ (f x) y) â‰¡ g y) (PO.glue y) extâ‰¡ â‰¡
    extâ‰¡
  lemma x g _ =
    elimÂ¹
      (Î» eq â†’
         subst (Î» y â†’ ext (injâ‚‚ x) g (âˆ‡ (f x) y) â‰¡ g y) eq extâ‰¡ â‰¡
         extâ‰¡ {x = injâ‚‚ x})
      (subst-refl _ _)
      _

-- If C is f-local, then Î»Â (gÂ :Â LocalisationÂ fÂ BÂ â†’Â C)Â â†’Â gÂ âˆ˜Â [_] is an
-- equivalence.

Localâ†’Is-equivalence-[] :
  f -Local C â†’
  Is-equivalence (Î» (g : Localisation f B â†’ C) â†’ g âˆ˜ [_])
Localâ†’Is-equivalence-[] {f = f} local =
  Localâ†’Is-equivalence-âˆ˜[] $
  _â‰ƒ_.from Localâ‰ƒIs-equivalence-âˆ˜Ã—Is-equivalence-âˆ˜âˆ‡ $
  P.[ (_â‰ƒ_.to Localâ‰ƒIs-equivalence-âˆ˜Ã—Is-equivalence-âˆ˜âˆ‡ Î» x â†’
                                          $âŸ¨ local x âŸ©
       Is-equivalence (_âˆ˜ f x)            â†’âŸ¨ Is-equivalenceâ‰ƒIs-equivalence-âˆ˜Ë¡
                                               (_â‰ƒ_.is-equivalence $ â†’-cong I.ext (Eq.â†”â‡’â‰ƒ $ inverse B.â†‘â†”) F.id) _ âŸ©â–¡
       Is-equivalence (_âˆ˜ (f x âˆ˜ lower))  â–¡)
    , (Î» x â†’
           (                             $âŸ¨ local x âŸ©
            Is-equivalence (_âˆ˜ f x)      â†’âŸ¨ PO.Is-equivalence-âˆ˜âˆ‡ âŸ©â–¡
            Is-equivalence (_âˆ˜ âˆ‡ (f x))  â–¡)
         , (                                 $âŸ¨ local x âŸ©
            Is-equivalence (_âˆ˜ f x)          â†’âŸ¨ PO.Is-equivalence-âˆ˜âˆ‡ âŸ©
            Is-equivalence (_âˆ˜ âˆ‡ (f x))      â†’âŸ¨ PO.Is-equivalence-âˆ˜âˆ‡ âŸ©â–¡
            Is-equivalence (_âˆ˜ âˆ‡ (âˆ‡ (f x)))  â–¡))
    ]
